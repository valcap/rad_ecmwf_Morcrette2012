SUBROUTINE RADLSWR &
 &( KIDIA, KFDIA , KLON , KLEV , KMODE , KAER , KAERO ,&
 &  PRII0,&
 &  PAER , PAERO , PALBD, PALBP, PAPH  , PAP  ,&
 &  PCCNL, PCCNO , PGELAM,PGEMU,&
 &  PCO2 , PCH4  , PN2O , PNO2 , PC11  , PC12 , PC22 , PCL4 ,&
 &  PCLFR, PDP   , PEMIS, PEMIW, PLSM  , PMU0 , POZON,&
 &  PQ   , PQIWP , PQLWP, PQS  , PQRAIN, PRAINT,&
 &  PTH  , PT    , PTS  , PNBAS, PNTOP,&
 &  PEMIT, PFCT  , PFLT , PFCS , PFLS  , PFRSOD, &
 &  PSUDU, PUVDF , PPARF, PPARCF, PTINCF )

!**** *RADLSWM* - INTERFACE TO ECMWF McICA LW AND SW RADIATION SCHEMES

!     PURPOSE.
!     --------
!           CONTROLS RADIATION COMPUTATIONS

!**   INTERFACE.
!     ----------

!        EXPLICIT ARGUMENTS :
!        --------------------
! PAER   : (KLON,6,KLEV)     ; OPTICAL THICKNESS OF THE AEROSOLS
! PAERP  : (KLON,6,KLEV)     ; OPTICAL THICKNESS OF THE PROGNOSTIC AEROSOLS
! PAERO  : (KLON,KLEV,NAERO) ; PROGNOSTIC AEROSOL MIXIN RATIO
! PALBD  : (KLON,NSW)        ; SURF. SW ALBEDO FOR DIFFUSE RADIATION
! PALBP  : (KLON,NSW)        ; SURF. SW ALBEDO FOR PARALLEL RADIATION
! PAPH   : (KLON,KLEV+1)     ; HALF LEVEL PRESSURE
! PAP    : (KLON,KLEV)       ; FULL LEVEL PRESSURE
! PCCNL  : (KLON)            ; CCN CONCENTRATION OVER LAND
! PCCNO  : (KLON)            ; CCN CONCENTRATION OVER OCEAN
! PCO2   : (KLON,KLEV)       ; CONCENTRATION IN CO2 (KG/KG)
! PCH4   : (KLON,KLEV)       ; CONCENTRATION IN CH4 (KG/KG)
! PN2O   : (KLON,KLEV)       ; CONCENTRATION IN N2O (KG/KG)
! PNO2   : (KLON,KLEV)       ; CONCENTRATION IN NO2 (KG/KG)
! PC11   : (KLON,KLEV)       ; CONCENTRATION IN CFC11 (KG/KG)
! PC12   : (KLON,KLEV)       ; CONCENTRATION IN CFC12 (KG/KG)
! PC22   : (KLON,KLEV)       ; CONCENTRATION IN CFC22 (KG/KG)
! PCL4   : (KLON,KLEV)       ; CONCENTRATION IN CCL4  (KG/KG)
! PCLFR  : (KLON,KLEV)       ; CLOUD FRACTIONAL COVER
! PDP    : (KLON,KLEV)       ; LAYER PRESSURE THICKNESS
! PEMIS  : (KLON)            ; SURFACE LW EMISSIVITY
! PEMIW  : (KLON)            ; SURFACE LW WINDOW EMISSIVITY
! PGELAM : (KLON)            ; LONGITUDE (RADIANS)
! PGEMU  : (KLON)            ; SINE OF LATITUDE
! PLSM   : (KLON)            ; LAND-SEA MASK
! PMU0   : (KLON)            ; SOLAR ANGLE
! PNBAS  : (KLON)            ; INDEX OF BASE OF CONVECTIVE LAYER
! PNTOP  : (KLON)            ; INDEX OF TOP OF CONVECTIVE LAYER
! POZON  : (KLON,KLEV)       ; OZONE AMOUNT in LAYER (KG/KG*PA)
! PQ     : (KLON,KLEV)       ; SPECIFIC HUMIDITY KG/KG
! PQIWP  : (KLON,KLEV)       ; SOLID  WATER KG/KG
! PQLWP  : (KLON,KLEV)       ; LIQUID WATER KG/KG
! PQS    : (KLON,KLEV)       ; SATURATION WATER VAPOR  KG/KG
! PQRAIN : (KLON,KLEV)       ; RAIN WATER KG/KG
! PRAINT : (KLON,KLEV)       ; RAIN RATE (m/s)
! PTH    : (KLON,KLEV+1)     ; HALF LEVEL TEMPERATURE
! PT     : (KLON,KLEV)       ; FULL LEVEL TEMPERATURE
! PTS    : (KLON)            ; SURFACE TEMPERATURE
!     ==== OUTPUTS ===
! PFCT   : (KLON,KLEV+1)     ; CLEAR-SKY LW NET FLUXES
! PFLT   : (KLON,KLEV+1)     ; TOTAL LW NET FLUXES
! PFCS   : (KLON,KLEV+1)     ; CLEAR-SKY SW NET FLUXES
! PFLS   : (KLON,KLEV+1)     ; TOTAL SW NET FLUXES
! PFRSOD : (KLON)            ; TOTAL-SKY SURFACE SW DOWNWARD FLUX
! PEMIT  : (KLON)            ; SURFACE TOTAL LONGWAVE EMISSIVITY
! PSUDU  : (KLON)            ; SOLAR RADIANCE IN SUN'S DIRECTION

!        IMPLICIT ARGUMENTS :   NONE
!        --------------------

!     METHOD.
!     -------
!        SEE DOCUMENTATION

!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------
!        ECMWF RESEARCH DEPARTMENT DOCUMENTATION OF THE IFS

!     AUTHORS.
!     --------
!        J.-J. MORCRETTE         *ECMWF*

!     MODIFICATIONS.
!     --------------
!        ORIGINAL : 88-02-04
!        J.-J. MORCRETTE 94-11-15 DIRECT/DIFFUSE SURFACE ALBEDO
!        08/96: J.-J. Morcrette/Ph. Dandin: tests of eff. radius param.
!        9909 : JJMorcrette effect.radius + inhomogeneity factors
!        JJMorcrette 990128 : sunshine duration
!        JJMorcrette : 990831 RRTM-140gp
!        JJMorcrette : 010112 Sun-Rikus ice particle Diameter
!        JJMorcrette : 010301 cleaning liq/ice cloud optical properties
!        JJMorcrette : 011005 CCN --> Re liquid water clouds
!        JJMorcrette : 011108 Safety checks
!        JJMorcrette : 011108 Safety checks
!        DJSalmond   : 020211 Check before R-To-R
!        JJMorcrette : 030430 Interface to SRTM-224gp
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        JJMorcrette 20060721 PP of clear-sky and TOA incident solar radiation
!        JJMorcrette : 060110 McICA approach to RRTM and SRTM-112g
!        JJMorcrette : 20071015 3D fields for CO2, CH4, N2O and NO2
!        JJMorcrette 20070321 Prognostic aerosols in radiation
!        R.Forbes    20071015 Add RDECORR_CF,RDECORR_CW
!        JJMorcrette 20080422 Prognostic aerosols in radiation
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE YOMCT0   , ONLY : NSTART
USE YOMCT3   , ONLY : NSTEP
USE YOMCST   , ONLY : RG       ,RD       ,RTT      ,RPI
USE YOECLD   , ONLY : RDECORR_CF       ,RDECORR_CW
USE YOECLDP  , ONLY : RCCNOM, RCCNSS , RCCNSU
USE YOERAD   , ONLY : NSW      ,NOVLP  ,LRRTM    ,&
 & LCCNL  , LCCNO  , LDIFFC , &
 & NRADIP , NRADLP , NICEOPT, NLIQOPT, &
 & RCCNLND, RCCNSEA, NMcICA , RMINICE, RRe2De
USE YOELW    , ONLY : NTRA     ,NUA
USE YOESW    , ONLY : REBCUG   ,REBCUH   ,&
 & RHSAVI   ,RFULIO   ,RFUETA  ,RFUETB   ,RFUETC   ,RLILIA   ,RLILIB

USE YOESRTCOP , ONLY : &
 & RSYFWA   ,RSYFWB   ,RSYFWC   ,RSYFWD   ,RSYFWE   ,RSYFWF  &
 & , RSECIA   ,RSECIB   ,RSECIC   ,RSECID   ,RSECIE   ,RSECIF &
 & , RSASWA   ,RSASWB   ,RSASWC   ,RSASWD   ,RSASWE   ,RSASWF &
 & , RSFUA0   ,RSFUA1   ,RSFUB0   ,RSFUB1   ,RSFUB2   ,RSFUB3 &
 & , RSFUC0   ,RSFUC1   ,RSFUC2   ,RSFUC3   &
 & , RSFLA0   ,RSFLA1   ,RSFLB0   ,RSFLB1   ,RSFLB2   ,RSFLB3 &
 & , RSFLC0   ,RSFLC1   ,RSFLC2   ,RSFLC3   ,RSFLD0   ,RSFLD1 &
 & , RSFLD2   ,RSFLD3

USE YOERDU   , ONLY : NUAER  ,NTRAER   ,REPLOG   ,REPSC    ,REPSCA  ,REPSCW   ,DIFF
USE YOETHF   , ONLY : RTICE
USE YOEPHLI  , ONLY : LPHYLIN
USE YOERRTFTR, ONLY : NGB
USE YOESRTWN , ONLY : NGBSW  ,NMPSRTM
USE YOEAERSRC, ONLY : LEPAERO
USE YOEAERATM, ONLY : LAEROPT, LAERRAD
USE YOM_YGFL , ONLY : NACTAERO

USE YOMLUN   , ONLY : NULOUT


IMPLICIT NONE

!-- McICA --------------------------------

INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KMODE
INTEGER(KIND=JPIM),INTENT(IN)    :: KAER ! Argument NOT used
INTEGER(KIND=JPIM),INTENT(IN)    :: KAERO
REAL(KIND=JPRB)                  :: PRII0 ! Argument NOT used
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAER(KLON,6,KLEV)
!!!! REAL(KIND=JPRB)   ,INTENT(IN)    :: PAERP(KLON,6,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAERO(KLON,KLEV,KAERO)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBD(KLON,NSW)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBP(KLON,NSW)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPH(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCCNL(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCCNO(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCO2(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCH4(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PN2O(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNO2(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PC11(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PC12(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PC22(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCL4(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCLFR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEMIS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEMIW(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGELAM(KLON), PGEMU(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POZON(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQIWP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLWP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KLON,KLEV) ! Argument NOT used
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQRAIN(KLON,KLEV) ! Argument NOT used
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAINT(KLON,KLEV) ! Argument NOT used
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTH(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNBAS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNTOP(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMIT(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCT(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFLT(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCS(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFLS(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSOD(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSUDU(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUVDF(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PPARF(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PPARCF(KLON), PTINCF(KLON)
!! REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDOWN(KLON,4)
!     -----------------------------------------------------------------

!*       0.1   ARGUMENTS.
!              ----------
!     ==== COMPUTED IN RADLSW ===
!     -----------------------------------------------------------------

!*       0.2   LOCAL ARRAYS.
!              -------------
!     -----------------------------------------------------------------

!-- ARRAYS FOR LOCAL VARIABLES -----------------------------------------

REAL(KIND=JPRB) :: &
 & ZTAUCLD(KLON,KLEV,16), ZTCLEAR(KLON)
REAL(KIND=JPRB) :: ZAER(KLON,6,KLEV) &
 & , ZCLDLW(KLON,KLEV)  , ZCLDSW(KLON,KLEV)&
 & , ZDT0(KLON)        &
 & , ZEMIS(KLON)        , ZEMIW(KLON)&
 & , ZFLUX (KLON,2,KLEV+1)                 , ZFLUC(KLON,2,KLEV+1)&
! & , ZFIWP(KLON,KLEV)   , ZFLWP(KLON,KLEV) , ZFRWP(KLON,KLEV)&
 & , ZFIWP(KLON,KLEV)   , ZFLWP(KLON,KLEV)&
 & , ZIWC(KLON,KLEV)    , ZLWC(KLON,KLEV)&
 !cc            , ZRWC(KLON)
 & , ZMU0(KLON)         , ZOZ(KLON,KLEV)   , ZOZN(KLON,KLEV)&
 & , ZPMB(KLON,KLEV+1)  , ZPSOL(KLON)&
 & , ZTAVE (KLON,KLEV)  , ZTL(KLON,KLEV+1)&
 & , ZVIEW(KLON)
REAL(KIND=JPRB) ::&
 & ZFCDWN(KLON,KLEV+1), ZFCUP(KLON,KLEV+1)&
 & , ZFSDWN(KLON,KLEV+1), ZFSUP(KLON,KLEV+1)&
 & , ZFSUPN(KLON)       , ZFSUPV(KLON)&
 & , ZFCUPN(KLON)       , ZFCUPV(KLON)&
 & , ZFSDNN(KLON)       , ZFSDNV(KLON)&
 & , ZFCDNN(KLON)       , ZFCDNV(KLON)
REAL(KIND=JPRB) ::&
 & ZDESR(KLON), ZRADIP(KLON)       , ZRADLP(KLON)    , &
 !cc           , ZRADRD(KLON)
 & ZRAINT(KLON)       , ZEMIT(KLON)
REAL(KIND=JPRB) :: ZSUDU(KLON), ZFUVF(KLON), ZFUVC(KLON), ZPARF(KLON), ZPARCF(KLON)

INTEGER(KIND=JPIM) :: IK, ILW16, ISW, ISW14, ITLW, ITSW
INTEGER(KIND=JPIM) :: JK, JKL, JKLP1, JKP1, JL, JRTM, JSW
INTEGER(KIND=JPIM) :: JAER

REAL(KIND=JPRB) :: ZASYMX, ZDIFFD, ZGI, ZGL, ZGR, ZIWGKG, ZLWGKG,&
 & ZMULTI, ZMULTL, ZOI, ZOL, &
 & ZOMGMX, ZOR, ZRMUZ, ZRWGKG, ZTAUD, ZTAUMX, ZTEMPC, &
 & ZTOI, ZTOL, ZTOR, ZDPOG, ZPODT
REAL(KIND=JPRB) :: ZALND, ZASEA, ZD, ZDEN, ZNTOT, ZNUM, Z1RADI, Z1RADL,&
 & ZBETAI, ZOMGI, ZOMGP, ZFDEL, ZTCELS, ZFSR, ZAIWC, ZBIWC, &
 & ZDefRe, ZRefDe, ZEXTCF, Z1MOMG, ZRSAIA, ZRSAID, ZRSAIE, ZRSAIF, ZRSAIG, ZRSALD

REAL(KIND=JPRB) :: ZSALBD(KLON,14)     , ZSALBP(KLON,14)     , ZALBT(KLON,14)
REAL(KIND=JPRB) :: ZSTAUC(KLON,14,KLEV), ZSASYC(KLON,14,KLEV), ZSOMGC(KLON,14,KLEV)
REAL(KIND=JPRB) :: ZFSUC(KLON,2,KLEV+1), ZFSUX(KLON,2,KLEV+1)

INTEGER(KIND=JPIM), PARAMETER :: ICOLSLW=140, ICOLSSW=112
INTEGER(KIND=JPIM) :: ICOLS, IJK, IPRINT(KLON,KLEV), JCOLS, ICLW, ICSW, JCLW, JCSW

REAL(KIND=JPRB)    :: ZCLFR(KLON,KLEV) , ZQIWP(KLON,KLEV) , ZQLWP(KLON,KLEV), ZETA(KLON,KLEV)
REAL(KIND=JPRB)    :: ZGLAT(KLON)      , ZGLON(KLON)
REAL(KIND=JPRB)    :: ZLC_CF(KLON,KLEV), ZLC_CW(KLON,KLEV), ZSIGQCW(KLON,KLEV), ZPS(KLON)
REAL(KIND=JPRB)    :: ZQI_SL(KLON,KLEV,ICOLSLW), ZQL_SL(KLON,KLEV,ICOLSLW)
REAL(KIND=JPRB)    :: ZQI_SS(KLON,KLEV,ICOLSSW), ZQL_SS(KLON,KLEV,ICOLSSW)
REAL(KIND=JPRB)    :: ZQILW(KLON,ICOLSLW,KLEV) , ZQLLW(KLON,ICOLSLW,KLEV)
REAL(KIND=JPRB)    :: ZQISW(KLON,ICOLSLW,KLEV) , ZQLSW(KLON,ICOLSLW,KLEV)
INTEGER(KIND=JPIM) :: ICLDLW(KLON), ICLDSW(KLON)

REAL(KIND=JPRB)    :: ZRADLI(KLON,KLEV), ZRADIC(KLON,KLEV), ZDESIC(KLON,KLEV)

REAL(KIND=JPRB)    :: ZFCLW(KLON,ICOLSLW,KLEV), ZTOLW(KLON,KLEV,ICOLSLW)
REAL(KIND=JPRB)    :: ZFCSW(KLON,ICOLSSW,KLEV) , ZTOSW(KLON,ICOLSSW,KLEV)
REAL(KIND=JPRB)    :: ZASSW(KLON,ICOLSSW,KLEV) , ZOMSW(KLON,ICOLSSW,KLEV)
REAL(KIND=JPRB)    :: ZSWFC(KLON,ICOLSSW,KLEV) , ZLWFC(KLON,ICOLSLW,KLEV)
INTEGER(KIND=JPIM) :: IFCLW(KLON,ICOLSLW,KLEV) , IFCSW(KLON,ICOLSSW,KLEV)

REAL(KIND=JPRB)    :: ZRG, ZTEMP, ZRPI

!* Indices and Arrays linked to prognostic aerosols
INTEGER(KIND=JPIM) :: IACTAERO, INWAVL, ITWAVL(17)
INTEGER(KIND=JPIM) :: JAERSS, JAERDD, JAEROM, JAERBC, JAERSU
REAL(KIND=JPRB)    :: ZCCN(KLON,KLEV), ZMAER(KLON,KLEV,5), ZMAERMN(5), ZAERO(KLON,KLEV,KAERO),&
  & ZRE_LIQ(KLON,KLEV), ZRHO(KLON,KLEV), ZTAUAER(KLON,6,KLEV), ZOMGAER(KLON,6,KLEV), ZASYAER(KLON,6,KLEV)



! Robert's Cloud generator
!  type(randomNumberStream) :: stream

! Raisanen and Barker's generator
!  REAL(KIND=JPRB) :: RLC_CF(klev), RLC_CW(klev), ZDROPS(klev), ZNUEFF(klev), ZRELW0(klev), ZREIW0(klev)
!  REAL(KIND=JPRB) :: ZF(klon,klev), ZRHOF(klon,klev), ZSIGQCW(klon,klev), ZCLDF(klev)
!  REAL(KIND=JPRB) :: YCF(ICOLSSW,klev), YQLW(ICOLSSW,klev), YQIW(ICOLSSW,klev), YRELW(ICOLSSW,klev), YREIW(ICOLSSW,klev)

LOGICAL :: LLCONST_RE, LLMAXRAN, LLPPH, LLPRINT

!-- end of McICA specific -----------------------

!     -----------------------------------------------------------------

!*         1.     PREPARATORY CALCULATIONS
!                 ------------------------

print *,'Just entered radlswr'

!          1.1    INITIALISATION OF FLUXES AND DIAGNOSTIC OUTPUT FIELDS
!                 -----------------------------------------------------

ZRG=1.0_JPRB/RG
ZFCUP(:,:) = 0.0_JPRB
ZFSUP(:,:) = 0.0_JPRB
ZFCDWN(:,:) = 0.0_JPRB
ZFSDWN(:,:) = 0.0_JPRB
ZFCDWN(:,KLEV+1)=REPLOG
ZFSDWN(:,KLEV+1)=REPLOG
ZFLUX(:,:,:) = 0.0_JPRB
ZFLUC(:,:,:) = 0.0_JPRB
ZFSUX(:,:,:) = 0.0_JPRB
ZFSUC(:,:,:) = 0.0_JPRB
ZFSDNN(:) = 0.0_JPRB
ZFSDNV(:) = 0.0_JPRB
ZFCDNN(:) = 0.0_JPRB
ZFCDNV(:) = 0.0_JPRB
ZFSUPN(:) = 0.0_JPRB
ZFSUPV(:) = 0.0_JPRB
ZFCUPN(:) = 0.0_JPRB
ZFCUPV(:) = 0.0_JPRB

ZPSOL(KIDIA:KFDIA) = PAPH(KIDIA:KFDIA,KLEV+1)
ZPMB(KIDIA:KFDIA,1) = ZPSOL(KIDIA:KFDIA) *0.01_JPRB
ZDT0(KIDIA:KFDIA) = PTS(KIDIA:KFDIA) - PTH(KIDIA:KFDIA,KLEV+1)
ZEMIT(:) = 0.0_JPRB
ZSUDU(:) = 0.0_JPRB
PSUDU(:) = 0.0_JPRB
PUVDF(:) = 0.0_JPRB
PPARF(:) = 0.0_JPRB
PPARCF(:)= 0.0_JPRB
ZEMIS(:) = PEMIS(:)
ZEMIW(:) = PEMIW(:)
ZMU0(:)  = PMU0(:)

!     -------------------------------------------------------------------------

!*         1.2    SET-UP INPUT QUANTITIES FOR ALL RADIATION CONFIGURATIONS
!                 --------------------------------------------------------

!-- in-cloud ice and water mixing ratios; cloud ice and liquid contents and paths

DO JK=1,KLEV
  IJK=KLEV+1-JK
  DO JL=KIDIA,KFDIA
    ZCLFR(JL,JK)=PCLFR(JL,JK)
    ZCLDSW(JL,IJK)=PCLFR(JL,JK)
    ZCLDLW(JL,IJK)=PCLFR(JL,JK)
    ZETA(JL,JK) =PAP(JL,JK)/PAPH(JL,KLEV+1)
    IF (PCLFR(JL,JK) >= 0.001_JPRB) THEN
      ZTEMP=1.0_JPRB/PCLFR(JL,JK)
      ZQIWP(JL,JK)=MAX(0._JPRB, PQIWP(JL,JK)*ZTEMP)
      ZQLWP(JL,JK)=MAX(0._JPRB, PQLWP(JL,JK)*ZTEMP)
      ZIWGKG=ZQIWP(JL,JK)*1000._JPRB
      ZLWGKG=ZQLWP(JL,JK)*1000._JPRB
    ELSE
      ZCLFR(JL,JK)=0._JPRB
      ZQIWP(JL,JK)=0._JPRB
      ZQLWP(JL,JK)=0._JPRB
      ZIWGKG=0._JPRB
      ZLWGKG=0._JPRB
    ENDIF
    ZRWGKG=0._JPRB
    ZRAINT(JL)=0._JPRB
    ZDPOG=PDP(JL,JK)*ZRG
    ZFIWP(JL,JK) = ZIWGKG*ZDPOG
    ZFLWP(JL,JK) = ZLWGKG*ZDPOG
!   ZFRWP(JL,JK) = ZRWGKG*ZDPOG  ! Always zero
    ZPODT = PAP(JL,JK)/(RD*PT(JL,JK))
    ZRHO(JL,JK) = ZPODT
    ZIWC(JL,JK) = ZIWGKG*ZPODT
    ZLWC(JL,JK) = ZLWGKG*ZPODT
  ENDDO
ENDDO
DO JL=KIDIA,KFDIA
  ZPS(JL)=PAPH(JL,KLEV+1)
ENDDO


!-- climatological aerosols
!
!   1 = land   = organic + sulphate
!   2 = sea    = sea salt
!   3 = desert = dust
!   4 = urban  = black carbon
!   5 = volcanic
!   6 = stratospheric background

DO JAER=1,6
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZAER(JL,JAER,JK)=PAER(JL,JAER,JK)
    ENDDO
  ENDDO
ENDDO


!-- prognostic aerosols
!  PAERO are entered in kg/kg
!  ZMAER are in ug / m3              ( micrograms per m**3 )
!  ZCNN  are in 1/cm3
!  Menon et al., 2002: JAS, 59, 692-713 Eqns 1a, 1b

IACTAERO = KAERO-1
!IACTAERO = NACTAERO
IF (LEPAERO .AND. IACTAERO >= 12) THEN
  DO JAER=1,IACTAERO
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZAERO(JL,JK,JAER)=MAX(0._JPRB,PAERO(JL,JK,JAER))
      ENDDO
    ENDDO
  ENDDO

  JAERSS=1
  JAEROM=2
  JAERBC=3
  JAERSU=4
  JAERDD=5
  ZMAERMN(JAERSS)=2.12E-10_JPRB*1.E9_JPRB
  ZMAERMN(JAERDD)=1.01E-09_JPRB*1.E9_JPRB
  ZMAERMN(JAEROM)=3.05E-11_JPRB*1.E9_JPRB
  ZMAERMN(JAERBC)=3.05E-11_JPRB*1.E9_JPRB
  ZMAERMN(JAERSU)=1.02E-09_JPRB*1.E9_JPRB
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZMAER(JL,JK,1) = ZAERO(JL,JK, 1)*ZRHO(JL,JK)*1.E9_JPRB
      ZMAER(JL,JK,2) = ZAERO(JL,JK, 7)*ZRHO(JL,JK)*1.E9_JPRB
      ZMAER(JL,JK,3) = ZAERO(JL,JK, 9)*ZRHO(JL,JK)*1.E9_JPRB
      ZMAER(JL,JK,4) = ZAERO(JL,JK,12)*ZRHO(JL,JK)*1.E9_JPRB
      ZMAER(JL,JK,5) = ZAERO(JL,JK, 4)*ZRHO(JL,JK)*1.E9_JPRB
      ZCCN(JL,JK)=10.0_JPRB**(2.41+&
! CCNxx factors: for org.matter 0.13, sea salt 0.05, and sulphate 0.50
                    & RCCNOM*LOG10(MAX(ZMAER(JL,JK,JAEROM),REPSCA)) +&
                    & RCCNSS*LOG10(MAX(ZMAER(JL,JK,JAERSS),REPSCA)) +&
                    & RCCNSU*LOG10(MAX(ZMAER(JL,JK,JAERSU),REPSCA)) )
      ZCCN(JL,JK) = MAX ( ZCCN(JL,JK), 50._JPRB)
      ZRE_LIQ(JL,JK) = (2.387e-10_JPRB*ZRHO(JL,JK)*ZQLWP(JL,JK)/ZCCN(JL,JK))**0.333_JPRB
    ENDDO
  ENDDO
  IF (LAERRAD) THEN
    INWAVL=1
    ITWAVL(:)=0
    ITWAVL(1)=8

    CALL AER_RAD ( &
     & KIDIA, KFDIA, KLON, 1 , KLEV , IACTAERO, INWAVL, ITWAVL, NSTART, NSTEP, &
     & PAP  , PAPH , PQ  , PT, ZAERO, &
     & ZTAUAER , ZOMGAER , ZASYAER  )

    IF (LAEROPT(3)) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,1,JK)=ZTAUAER(JL,3,JK)                  ! organic
        ENDDO
      ENDDO
    ENDIF
    IF (LAEROPT(5)) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,1,JK)=ZTAUAER(JL,5,JK)                  ! sulphate
        ENDDO
      ENDDO
    ENDIF
    IF (LAEROPT(3).AND.LAEROPT(5)) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,1,JK)=ZTAUAER(JL,3,JK)+ZTAUAER(JL,5,JK) ! organic + sulphate
        ENDDO
      ENDDO
    ENDIF
    IF (LAEROPT(1)) THEN                                  !  sea salt
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,2,JK)=ZTAUAER(JL,1,JK)
        ENDDO
      ENDDO
    ENDIF
    IF (LAEROPT(2)) THEN                                  ! desert dust
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,3,JK)=ZTAUAER(JL,2,JK)
        ENDDO
      ENDDO
    ENDIF
    IF (LAEROPT(4)) THEN                                  ! black carbon
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,4,JK)=ZTAUAER(JL,4,JK)
        ENDDO
      ENDDO
    ENDIF
  ENDIF
ENDIF

!IF (NSTEP == NSTART) THEN
  WRITE(NULOUT,9010) LEPAERO,LAERRAD,(LAEROPT(JAER),JAER=1,5)
9010 FORMAT(1x,'radlswr aer: LEPAERO=',L2,'  LAERRAD=',L2,'  LAEROPT(1-..)=',8L3)
  DO JK=30,KLEV
    WRITE(NULOUT,9001) JK,((ZAER(JL,JAER,JK),JAER=1,6),JL=KIDIA,KFDIA,5)
9001 FORMAT(1X,'radlswr aer',I3,(18E9.2))
  ENDDO
!ENDIF

!     -------------------------------------------------------------------------

!*         1.3    EFFECTIVE RADII AND PARTICLE SIZE
!                 ---------------------------------

ZRefDe = RRe2De
ZDefRe = 1.0_JPRB / ZRefDe

DO JK=1,KLEV
  DO JL = KIDIA,KFDIA
! --- EFFECTIVE RADIUS FOR WATER, ICE AND RAIN PARTICLES

! very old parametrization as f(pressure)

    IF (NRADLP == 0) THEN
!-- very old parametrization as f(pressure) ERA-15
      ZRADLP(JL)=10.0_JPRB + (100000.0_JPRB-PAP(JL,JK))*3.5_JPRB

    ELSEIF (NRADLP == 1) THEN
! simple distinction between land (10) and ocean (13) Zhang and Rossow
      IF (PLSM(JL) < 0.5_JPRB) THEN
        ZRADLP(JL)=13.0_JPRB
      ELSE
        ZRADLP(JL)=10.0_JPRB
      ENDIF

    ELSEIF (NRADLP == 2) THEN
!--  based on Martin et al., 1994, JAS
      IF (PLSM(JL) < 0.5_JPRB) THEN
        IF (LCCNO) THEN
!          ZASEA=50.0_JPRB
          ZASEA=PCCNO(JL)
        ELSE
          ZASEA=RCCNSEA
        ENDIF
        ZD=0.33_JPRB
        ZNTOT=-1.15E-03_JPRB*ZASEA*ZASEA+0.963_JPRB*ZASEA+5.30_JPRB
      ELSE
        IF (LCCNL) THEN
!          ZALND=900.0_JPRB
          ZALND=PCCNL(JL)
        ELSE
          ZALND=RCCNLND
        ENDIF
        ZD=0.43_JPRB
        ZNTOT=-2.10E-04_JPRB*ZALND*ZALND+0.568_JPRB*ZALND-27.9_JPRB
      ENDIF
      ZNUM=3.0_JPRB*ZLWC(JL,JK)*(1.0_JPRB+3.0_JPRB*ZD*ZD)**2
      ZDEN=4.0_JPRB*RPI*ZNTOT*(1.0_JPRB+ZD*ZD)**3
      ZTEMP=1.0_JPRB/ZDEN
      IF((ZNUM*ZTEMP) > REPLOG)THEN
        ZRADLP(JL)=100.*EXP(0.333*LOG(ZNUM*ZTEMP))
        ZRADLP(JL)=MAX(ZRADLP(JL), 4.0_JPRB)
        ZRADLP(JL)=MIN(ZRADLP(JL),16.0_JPRB)
      ELSE
        ZRADLP(JL)=4.0_JPRB
      ENDIF
    ELSEIF (NRADLP == 3) THEN
!- effective radius of droplets linked to prognostic aerosol
!  using Memon et al's CCN (see above)
      ZRADLP(JL) = ZRE_LIQ(JL,JK)
      ZRADLP(JL)=MAX(ZRADLP(JL), 4.0_JPRB)
      ZRADLP(JL)=MIN(ZRADLP(JL),16.0_JPRB)
    ENDIF
    ZRADLI(JL,JK)=ZRADLP(JL)
  ENDDO
  DO JL = KIDIA,KFDIA

! diagnosing the ice particle effective radius/diameter

!- ice particle effective radius =f(T) from Liou and Ou (1994)

    IF (PT(JL,JK) < RTICE) THEN
      ZTEMPC=PT(JL,JK)-RTT
    ELSE
      ZTEMPC=RTICE-RTT
    ENDIF
    ZRADIP(JL)=326.3_JPRB+ZTEMPC*(12.42_JPRB + ZTEMPC*(0.197_JPRB + ZTEMPC*&
      & 0.0012_JPRB))

    IF (NRADIP == 0) THEN
!-- fixed 40 micron effective radius
      ZRADIP(JL)= 40.0_JPRB
      ZDESR(JL)= ZdefRe * ZRADIP(JL)

    ELSEIF (NRADIP == 1) THEN
!-- old formulation based on Liou & Ou (1994) temperature (40-130microns)
      ZRADIP(JL)=MAX(ZRADIP(JL), 40.0_JPRB)
      ZRADIP(JL)=MIN(ZRADIP(JL),130.0_JPRB)
      ZDESR(JL)= ZdefRe * ZRADIP(JL)

    ELSEIF (NRADIP == 2) THEN
!-- formulation following Jakob, Klein modifications to ice content
      ZRADIP(JL)=MAX(ZRADIP(JL),30.0_JPRB)
      ZRADIP(JL)=MIN(ZRADIP(JL),60.0_JPRB)
      ZDESR(JL)= ZDefRe * ZRADIP(JL)

    ELSEIF (NRADIP == 3  ) THEN
!- ice particle effective radius =f(T,IWC) from Sun and Rikus (1999)
! revised by Sun (2001)
      IF (ZIWC(JL,JK) > 0.0_JPRB ) THEN
        ZTEMPC = PT(JL,JK)-83.15_JPRB
        ZTCELS = PT(JL,JK)-RTT
        ZFSR = 1.2351_JPRB +0.0105_JPRB * ZTCELS
! Sun, 2001 (corrected from Sun & Rikus, 1999)
        ZAIWC = 45.8966_JPRB * ZIWC(JL,JK)**0.2214_JPRB
        ZBIWC = 0.7957_JPRB * ZIWC(JL,JK)**0.2535_JPRB
        ZDESR(JL) = ZFSR * (ZAIWC + ZBIWC*ZTEMPC)
        ZDESR(JL) = MIN ( MAX( ZDESR(JL), RMINICE), 155.0_JPRB)
        ZRADIP(JL)= ZRefDe * ZDESR(JL)
      ELSE
        ZDESR(JL) = 80.0_JPRB
        ZRADIP(JL)= ZRefDe * ZDESR(JL)
      ENDIF
    ENDIF

    ZRADIC(JL,JK)=ZRADIP(JL)
    ZDESIC(JL,JK)=ZDESR(JL)
  ENDDO
ENDDO
print *,'RADLSWR after radius and particle sizes'

!     -------------------------------------------------------------------------

!*         2.0    PREPARATION FOR McICA COMPUTATIONS
!                 ----------------------------------

!-- initialise all arrays to zero

! ZFCLW(:,:,:)=0._JPRB
! ZLWFC(:,:,:)=0._JPRB
! ZQILW(:,:,:)=0._JPRB
! ZQLLW(:,:,:)=0._JPRB
! ZTOLW(:,:,:)=0._JPRB

! ZFCSW(:,:,:)=0._JPRB
! ZSWFC(:,:,:)=0._JPRB
! ZQISW(:,:,:)=0._JPRB
! ZQLSW(:,:,:)=0._JPRB
! ZTOSW(:,:,:)=0._JPRB
! ZASSW(:,:,:)=0._JPRB
! ZOMSW(:,:,:)=0._JPRB

IF (NMcICA /= 0) THEN
  ZRPI=1.0_JPRB/RPI
  DO JL=KIDIA,KFDIA
    ZGLON(JL)=PGELAM(JL)*180._JPRB*ZRPI
    ZGLAT(JL)=ASIN(PGEMU(JL))*180._JPRB*ZRPI
  ENDDO



!*         2.1    SPECIFYING THE McICA CONFIGURATION
!                 ----------------------------------

  IF (NMcICA == 1) THEN

!-- equivalent PPH_MAXRAN

    LLCONST_RE= .true.
    LLMAXRAN  = .true.
    LLPPH     = .true.

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZLC_CF(JL,JK) = RDECORR_CF    ! decorrelation depth for cloud fraction
        ZLC_CW(JL,JK) = RDECORR_CW    ! decorrelation depth for condensate
        ZSIGQCW(JL,JK)= 0._JPRB
!        ZDROPS(JK) = 100.0_JPRB    ! droplet concentration (cm^-3)
!        ZNUEFF(JK) = 0.1_JPRB      ! effective variance of drop size dist'n
!        ZRELW0(JK)  = 10.0_JPRB    ! droplet effective radius
!        ZREIW0(JK)  = 40.0_JPRB    ! ice crystal effective "radius"
      ENDDO
    ENDDO

  ELSEIF (NMcICA == 2) THEN

!-- equivalent GWTSA with Nu=1

    LLCONST_RE= .true.
    LLMAXRAN  = .false.
    LLPPH     = .false.

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZLC_CF(JL,JK) = RDECORR_CF    ! decorrelation depth for cloud fraction
        ZLC_CW(JL,JK) = RDECORR_CW    ! decorrelation depth for condensate
        ZSIGQCW(JL,JK)= 1._JPRB
!        ZDROPS(JK) = 0.0_JPRB! droplet concentration (cm^-3)
!        ZNUEFF(JK) = 0.0_JPRB! effective variance of drop size dist'n
!        ZRELW0(JK)  = 10.0_JPRB! droplet effective radius
!        ZREIW0(JK)  = 40.0_JPRB! ice crystal effective "radius"
      ENDDO
    ENDDO
  ENDIF
  print *,'RADLSWR after setting up McICA'


!     -----------------------------------------------------------------

!*         2.2    CLOUD PARAMETERS FOR McICA COMPUTATIONS
!                 ---------------------------------------

! Before starting, copy original cloud fields for RRTM_LW and RRTM_SW
! done on cloud fraction and condensed ice and liquid water

!-- interface for Raisanen, Cole, Barker's cloud generator
!   it works from top to bottom:
!   inputs are ZCLFR, ZQLWP, ZQIWP
!   outputs are ZQL_SL, ZQI_SL, and ICLDLW an index 0 or 1 for each layer and each g-point

!-- call cloud generator to create ICOLS=ICOLSLW for LW computations
  CALL McICA_CLD_GEN &
    &( ZCLFR  , ZQLWP, ZQIWP, ZLC_CF, ZLC_CW, ZGLAT , ZGLON, &
    & ZSIGQCW, PT   , ZETA , ZPS   , KLON  , KIDIA , KFDIA, KLEV, ICOLSLW, &
    & LLPPH   , LLMAXRAN, &
    & ICLDLW , ZQL_SL, ZQI_SL )

!  JL=KIDIA
!  DO JK=1,KLEV
!    IF (ZCLFR(JL,JK) > 0._JPRB) THEN
!      PRINT 9002,JK,ZCLFR(JL,JK),(ZQL_SL(JL,JK,ITLW),ITLW=1,ICOLSLW)
!      PRINT 9002,JK,ZCLFR(JL,JK),(ZQI_SL(JL,JK,ITLW),ITLW=1,ICOLSLW)
!9002  FORMAT(1X,I3,F7.4,(18E10.3))
!    ENDIF
!  ENDDO

  DO JK=1,KLEV
    IJK=KLEV+1-JK
    DO JL=KIDIA,KFDIA
      IF (ICLDLW(JL) /= 0) THEN
        IPRINT(JL,JK)=0
      ENDIF
    ENDDO
    DO JCOLS=1,ICOLSLW
      DO JL=KIDIA,KFDIA
        IF (ICLDLW(JL) /= 0) THEN
          ZFCLW(JL,JCOLS,JK)=0._JPRB
          IF (ZQL_SL(JL,JK,JCOLS)+ZQI_SL(JL,JK,JCOLS) > 0._JPRB) THEN
            IFCLW(JL,JCOLS,JK)=1
            IPRINT(JL,JK)=IPRINT(JL,JK)+IFCLW(JL,JCOLS,JK)
            ZFCLW(JL,JCOLS,JK)=1._JPRB
            ZLWFC(JL,JCOLS,IJK)=1._JPRB
            ZQILW(JL,JCOLS,JK)=ZQI_SL(JL,JK,JCOLS)
            ZQLLW(JL,JCOLS,JK)=ZQL_SL(JL,JK,JCOLS)
          ELSE
            ZLWFC(JL,JCOLS,IJK)=0._JPRB
            ZQILW(JL,JCOLS,JK)=0._JPRB
            ZQLLW(JL,JCOLS,JK)=0._JPRB
          ENDIF

        ELSE
          ZFCLW(JL,JCOLS,JK)=0._JPRB
          ZLWFC(JL,JCOLS,IJK)=0._JPRB
          ZQILW(JL,JCOLS,JK)=0._JPRB
          ZQLLW(JL,JCOLS,JK)=0._JPRB
        ENDIF
      ENDDO
    ENDDO
  ENDDO

!-- call cloud generator to create ICOLS=ICOLSSW for SW computations
  CALL McICA_CLD_GEN &
    &( ZCLFR  , ZQLWP, ZQIWP, ZLC_CF, ZLC_CW, ZGLAT , ZGLON, &
    & ZSIGQCW, PT   , ZETA , ZPS   , KLON  , KIDIA , KFDIA, KLEV, ICOLSSW, &
    & LLPPH   , LLMAXRAN, &
    & ICLDSW , ZQL_SS, ZQI_SS )

  DO JK=1,KLEV
    IJK=KLEV+1-JK
    DO JL=KIDIA,KFDIA
      IF (ICLDSW(JL) /= 0) THEN
        IPRINT(JL,JK)=0
      ENDIF
    ENDDO
    DO JCOLS=1,ICOLSSW
      DO JL=KIDIA,KFDIA
        IF (ICLDSW(JL) /= 0) THEN
!-- please note this is the correct way: the initial bug in 32R1 has been corrected
          ZFCSW(JL,JCOLS,JK)=0._JPRB
          IF (ZQL_SS(JL,JK,JCOLS)+ZQI_SS(JL,JK,JCOLS) > 0._JPRB) THEN
            IFCSW(JL,JCOLS,JK)=1
            IPRINT(JL,JK)=IPRINT(JL,JK)+IFCSW(JL,JCOLS,JK)
            ZFCSW(JL,JCOLS,JK)=1._JPRB
            ZSWFC(JL,JCOLS,IJK)=1._JPRB
            ZQISW(JL,JCOLS,JK)=ZQI_SS(JL,JK,JCOLS)
            ZQLSW(JL,JCOLS,JK)=ZQL_SS(JL,JK,JCOLS)
          ELSE
            ZSWFC(JL,JCOLS,IJK)=0._JPRB
            ZQISW(JL,JCOLS,JK)=0._JPRB
            ZQLSW(JL,JCOLS,JK)=0._JPRB
          ENDIF
        ELSE
          ZFCSW(JL,JCOLS,JK)=0._JPRB
          ZSWFC(JL,JCOLS,IJK)=0._JPRB
          ZQISW(JL,JCOLS,JK)=0._JPRB
          ZQLSW(JL,JCOLS,JK)=0._JPRB
        ENDIF
      ENDDO
    ENDDO
  ENDDO

  ICLW=140
  ICSW=112

ELSE

!-- normal non-McICA calculations
  ILW16=16
!  DO JCLW=1,ILW16
!    DO JK=1,KLEV
!      IJK=KLEV+1-JK
!      DO JL=KIDIA,KFDIA
!!        ZCLDLW(JL,IJK)   =PCLFR(JL,JK)
!!        ZFCLW(JL,JCLW,JK)=PCLFR(JL,JK)
!!        ZQILW(JL,JCLW,JK)=ZQIWP(JL,JK)
!!        ZQLLW(JL,JCLW,JK)=ZQLWP(JL,JK)
!      ENDDO
!    ENDDO
!  ENDDO

  ISW14=14
!  DO JCSW=1,ISW14
!    DO JK=1,KLEV
!      IJK=KLEV+1-JK
!      DO JL=KIDIA,KFDIA
!!       ZCLDSW(JL,IJK)   =PCLFR(JL,JK)
!!       ZFCSW(JL,JCSW,JK)=PCLFR(JL,JK)
!!       ZQISW(JL,JCSW,JK)=ZQIWP(JL,JK)
!!       ZQLSW(JL,JCSW,JK)=ZQLWP(JL,JK)
!      ENDDO
!    ENDDO
!  ENDDO
ENDIF
print *,'RADLSWR after defining cloudiness a la McICA'
LLPRINT=.FALSE.

!     -----------------------------------------------------------------

!*         3.0    INITIALIZE VARIOUS OTHER FIELDS
!                 -------------------------------

!*         3.1    DIFFUSIVITY FACTOR OR SATELLITE VIEWING ANGLE
!                 ---------------------------------------------

DO JL = KIDIA,KFDIA
  ZVIEW(JL) = DIFF
ENDDO

!*         3.2    SURFACE ALBEDO
!                 --------------

!-- mapping SW[1:6] surface albedo into albedo for SRTM[1:14]
ISW=14
DO JSW=1,ISW
  ISW14=NMPSRTM(JSW)
  DO JL = KIDIA,KFDIA
    ZSALBD(JL,JSW)=PALBD(JL,ISW14)
    ZSALBP(JL,JSW)=PALBP(JL,ISW14)
  ENDDO
ENDDO

!*        3.3     PUTTING FIELDS UPSIDE DOWN
!                 --------------------------

DO JK = 1 , KLEV
  JKP1 = JK + 1
  JKL = KLEV+ 1 - JK
  JKLP1 = JKL + 1
  DO JL = KIDIA,KFDIA
    ZPMB(JL,JK+1)=PAPH(JL,JKL)*0.01_JPRB

!-- ZOZ in cm.atm for SW scheme
    ZOZ(JL,JK)   = POZON(JL,JKL) * 46.6968_JPRB *ZRG
    ZTL(JL,JK)=PTH(JL,JKLP1)
    ZTAVE(JL,JK)=PT(JL,JKL)
  ENDDO
ENDDO

DO JL=KIDIA,KFDIA
  ZTL(JL,KLEV+1)= PTH(JL,1)
  ZPMB(JL,KLEV+1) = PAPH(JL,1)*0.01_JPRB
ENDDO

!     ------------------------------------------------------------------

!*         4.     CLOUD AND AEROSOL PARAMETERS FOR REGULAR VERSIONS OF RT CODES
!                 ---------------------------- --------------------------------

IF (NMCICA == 0) THEN

!          4.1    INITIALIZE OPTICAL PROPERTIES TO CLEAR SKY VALUES
!                 -------------------------------------------------

  ZTAUCLD(:,:,:)=0.0_JPRB
  ZSTAUC(:,:,:)= 0.0_JPRB
  ZSOMGC(:,:,:)= 1.0_JPRB
  ZSASYC(:,:,:)= 0.0_JPRB

  ISW14=14

  DO JK = 1 , KLEV
    IJK=KLEV+1-JK


!          4.2    CLOUD SHORTWAVE OPTICAL PROPERTIES
!                 ----------------------------------
!   -------------------------
! --+ SW OPTICAL PARAMETERS +  Water clouds after Fouquart (1987)
!   -------------------------  Ice clouds (Ebert, Curry, 1992)

    DO JSW=1,ISW14
      DO JL = KIDIA,KFDIA
        ZTOL=0.0_JPRB
        ZGL =0.0_JPRB
        ZOL =0.0_JPRB
        ZTOI=0.0_JPRB
        ZGI =0.0_JPRB
        ZOI =0.0_JPRB
        ZTOR=0.0_JPRB
        ZGR =0.0_JPRB
        ZOR =0.0_JPRB
!       IF (ZFLWP(JL,JK) > REPSCW .OR. ZFIWP(JL,JK) > REPSCW .OR. ZFRWP(JL,JK) > REPSCW ) THEN
        IF (ZFLWP(JL,JK) > REPSCW .OR. ZFIWP(JL,JK) > REPSCW) THEN
          IF (ZFLWP(JL,JK) > REPSCW ) THEN
            IF (NLIQOPT /= 0 ) THEN
!-- SW: Slingo, 1989
              ZTOL = ZFLWP(JL,JK)*(RSASWA(JSW)+RSASWB(JSW)/ZRADLI(JL,JK))
              ZGL  = RSASWE(JSW)+RSASWF(JSW)*ZRADLI(JL,JK)
              ZOL  = 1. - RSASWC(JSW)-RSASWD(JSW)*ZRADLI(JL,JK)
            ELSE
!-- SW: Fouquart, 1991
              ZTOL = ZFLWP(JL,JK)*(RSYFWA(JSW)+RSYFWB(JSW)/ZRADLI(JL,JK))
              ZGL  = RSYFWF(JSW)
              ZOL  = RSYFWC(JSW)-RSYFWD(JSW)*EXP(-RSYFWE(JSW)*ZTOL)
            ENDIF
          ENDIF

          IF (ZFIWP(JL,JK) > REPSCW ) THEN
            IF (NICEOPT <= 1) THEN
!-- SW: Ebert-Curry
              ZTOI = ZFIWP(JL,JK)*(RSECIA(JSW)+RSECIB(JSW)/ZRADIC(JL,JK))
              ZGI  = RSECIE(JSW)+RSECIF(JSW)*ZRADIC(JL,JK)
              ZOI  = 1.0_JPRB - RSECIC(JSW)-RSECID(JSW)*ZRADIC(JL,JK)

            ELSEIF (NICEOPT == 2) THEN
!-- SW: Fu-Liou 1993
              Z1RADI = 1.0_JPRB / ZDESIC(JL,JK)
              ZBETAI = RSFLA0(JSW)+Z1RADI* RSFLA1(JSW)
              ZTOI = ZFIWP(JL,JK) * ZBETAI
              ZOMGI= RSFLB0(JSW)+ZRADIC(JL,JK)*(RSFLB1(JSW) + ZRADIC(JL,JK) &
               & *(RSFLB2(JSW)+ZRADIC(JL,JK)* RSFLB3(JSW) ))
              ZOI  = 1.0_JPRB - ZOMGI
              ZOMGP= RSFLC0(JSW)+ZRADIC(JL,JK)*(RSFLC1(JSW) + ZRADIC(JL,JK) &
               & *(RSFLC2(JSW)+ZRADIC(JL,JK)* RSFLC3(JSW) ))
              ZFDEL= RSFLD0(JSW)+ZRADIC(JL,JK)*(RSFLD1(JSW) + ZRADIC(JL,JK) &
               & *(RSFLD2(JSW)+ZRADIC(JL,JK)* RSFLD3(JSW) ))
              ZGI  = ((1.0_JPRB-ZFDEL)*ZOMGP + ZFDEL*3.0_JPRB) *0.33333_JPRB

            ELSEIF (NICEOPT == 3) THEN
!-- SW: Fu 1996
              Z1RADI = 1.0_JPRB / ZDESIC(JL,JK)
              ZBETAI = RSFUA0(JSW)+Z1RADI* RSFUA1(JSW)
              ZTOI = ZFIWP(JL,JK) * ZBETAI
              ZOMGI= RSFUB0(JSW)+ZDESIC(JL,JK)*(RSFUB1(JSW) + ZDESIC(JL,JK) &
               & *(RSFUB2(JSW)+ZDESIC(JL,JK)* RSFUB3(JSW) ))
              ZOI  = 1.0_JPRB - ZOMGI
              ZGI  = RSFUC0(JSW)+ZDESIC(JL,JK)*(RSFUC1(JSW) + ZDESIC(JL,JK) &
               & *(RSFUC2(JSW)+ZDESIC(JL,JK)* RSFUC3(JSW) ))
              ZGI  = MIN(1.0_JPRB, ZGI)
            ENDIF
          ENDIF

!  - MIX of WATER and ICE CLOUDS
          ZTAUMX= ZTOL + ZTOI + ZTOR
          ZOMGMX= ZTOL*ZOL + ZTOI*ZOI + ZTOR*ZOR
          ZASYMX= ZTOL*ZOL*ZGL + ZTOI*ZOI*ZGI + ZTOR*ZOR*ZGR

          ZASYMX= ZASYMX/ZOMGMX
          ZOMGMX= ZOMGMX/ZTAUMX

! --- SW FINAL CLOUD OPTICAL PARAMETERS

          ZSTAUC(JL,JSW,IJK) = ZTAUMX
          ZSOMGC(JL,JSW,IJK) = ZOMGMX
          ZSASYC(JL,JSW,IJK) = ZASYMX
        ENDIF
      ENDDO
    ENDDO


!          4.3    CLOUD LONGWAVE OPTICAL PROPERTIES FOR RRTM
!                 ------------------------------------------

!   -------------------------
! --+ LW OPTICAL PARAMETERS +  Water (and Ice) from Savijarvi (1998)
!   -------------------------  Ice clouds (Ebert, Curry, 1992)

! No need for a fixed diffusivity factor, accounted for spectrally below
! The detailed spectral structure does not require defining upward and
! downward effective optical properties

    ILW16=16

    DO JRTM=1,ILW16
      DO JL = KIDIA,KFDIA
        ZRSALD = 0.0_JPRB
        ZRSAID = 0.0_JPRB

        IF (ZFLWP(JL,JK)+ZFIWP(JL,JK) > REPSCW) THEN

          IF (NLIQOPT == 0 .OR. NLIQOPT >= 3) THEN
! water cloud total emissivity a la Smith and Shi (1992)
            ZMULTL=1.2_JPRB-0.006_JPRB*ZRADLI(JL,JK)
            ZRSALD= 0.144_JPRB*ZMULTL * 0.60240_JPRB

          ELSEIF (NLIQOPT == 1) THEN
! water cloud spectral emissivity a la Savijarvi (1997)
            ZRSALD= RHSAVI(JRTM,1) + ZRADLI(JL,JK)&
             & *(RHSAVI(JRTM,2) + ZRADLI(JL,JK)*RHSAVI(JRTM,3))

          ELSEIF (NLIQOPT == 2) THEN
! water cloud spectral emissivity a la Lindner and Li (2000)
            Z1RADL = 1.0_JPRB / ZRADLI(JL,JK)
            ZEXTCF = RLILIA(JRTM,1)+ZRADLI(JL,JK)*RLILIA(JRTM,2)+ Z1RADL*&
             & (RLILIA(JRTM,3) + Z1RADL*(RLILIA(JRTM,4) + Z1RADL*&
             & RLILIA(JRTM,5) ))
            Z1MOMG = RLILIB(JRTM,1) + Z1RADL*RLILIB(JRTM,2) &
             & + ZRADLI(JL,JK) *(RLILIB(JRTM,3) + ZRADLI(JL,JK)*RLILIB(JRTM,4) )
            ZRSALD = Z1MOMG * ZEXTCF
          ENDIF

          IF (NICEOPT == 0) THEN
! ice cloud spectral emissivity a la Smith & Shi (1992)
            ZMULTI=1.2_JPRB-0.006_JPRB*ZRADIC(JL,JK)
            ZRSAID= 0.103_JPRB*ZMULTI * 0.60240_JPRB

          ELSEIF (NICEOPT == 1) THEN
! ice cloud spectral emissivity a la Ebert-Curry (1992)
            ZRSAID= REBCUH(JRTM)+REBCUG(JRTM)/ZRADIC(JL,JK)

          ELSEIF (NICEOPT == 2) THEN
! ice cloud spectral emissivity a la Fu & Liou (1993)
            Z1RADI= 1.0_JPRB / ZDESIC(JL,JK)
            ZRSAID = RFULIO(JRTM,1) + Z1RADI &
             & *(RFULIO(JRTM,2) + Z1RADI*RFULIO(JRTM,3))

          ELSEIF (NICEOPT == 3) THEN
! ice cloud spectral emissivity a la Fu et al (1998) including
! parametrisation for LW scattering effect
            Z1RADI = 1.0_JPRB / ZDESIC(JL,JK)
            ZRSAIE = RFUETA(JRTM,1) + Z1RADI&
             & *(RFUETA(JRTM,2) + Z1RADI*RFUETA(JRTM,3))
            ZRSAIA = Z1RADI*(RFUETB(JRTM,1) +ZDESIC(JL,JK)*( RFUETB(JRTM,2) +ZDESIC(JL,JK)*&
             & ( RFUETB(JRTM,3) +ZDESIC(JL,JK)* RFUETB(JRTM,4))))
            ZRSAIG = RFUETC(JRTM,1) +ZDESIC(JL,JK)*( RFUETC(JRTM,2) +ZDESIC(JL,JK)*&
             & ( RFUETC(JRTM,3) +ZDESIC(JL,JK)* RFUETC(JRTM,4)))
            ZRSAIF = 0.5_JPRB + ZRSAIG*( 0.3738_JPRB + ZRSAIG*( 0.0076_JPRB + ZRSAIG*0.1186_JPRB ) )
            ZRSAID = (1.0_JPRB - ZRSAIA/ZRSAIE * ZRSAIF) * ZRSAIE
          ENDIF

          ZTAUD = ZRSALD*ZFLWP(JL,JK)+ZRSAID*ZFIWP(JL,JK)

! Diffusivity correction within clouds a la Savijarvi
          IF (LDIFFC) THEN
            ZDIFFD=MIN(MAX(1.517_JPRB-0.156_JPRB*LOG(ZTAUD) , 1.0_JPRB) , 2.0_JPRB)
          ELSE
            ZDIFFD=1.66_JPRB
          ENDIF
          ZTAUCLD(JL,IJK,JRTM) = ZTAUD*ZDIFFD
        ENDIF

      ENDDO
    ENDDO
  ENDDO

  ZTOLW(:,:,:) = 0.0_JPRB
  ZTOSW(:,:,:) = 0.0_JPRB
  ZASSW(:,:,:) = 0.0_JPRB
  ZOMSW(:,:,:) = 0.0_JPRB

ELSE


!          5.1    INITIALIZE OPTICAL PROPERTIES TO CLEAR SKY VALUES
!                 -------------------------------------------------

  ZTOLW(:,:,:) = 0.0_JPRB
! ZTOSW(:,:,:) = 0.0_JPRB
! ZASSW(:,:,:) = 0.0_JPRB
! ZOMSW(:,:,:) = 1.0_JPRB
  DO JK=1,KLEV
    DO JCSW=ICSW+1,ICOLSSW
      DO JL = KIDIA,KFDIA
        ZTOSW(JL,JCSW,JK) = 0.0_JPRB
        ZASSW(JL,JCSW,JK) = 0.0_JPRB
        ZOMSW(JL,JCSW,JK) = 1.0_JPRB
      ENDDO
    ENDDO
  ENDDO

!          5.2    SHORTWAVE OPTICAL PROPERTIES
!                 ----------------------------

  DO JCSW=1,ICSW
    JSW=NGBSW(JCSW)-15

    DO JK = 1 , KLEV
      IJK=KLEV+1-JK

!          5.3    CLOUD ICE AND LIQUID CONTENT AND PATH
!                 -------------------------------------

      DO JL = KIDIA,KFDIA

! --- LIQUID WATER CONTENT (g.m-3) AND LIQUID WATER PATH (g.m-2)
!       IF (ZFCSW(JL,JCSW,JK) > REPSC ) THEN
!         ZLWGKG=MAX(ZQLSW(JL,JCSW,JK)*1000.0_JPRB,0.0_JPRB)
!         ZIWGKG=MAX(ZQISW(JL,JCSW,JK)*1000.0_JPRB,0.0_JPRB)
!       ELSE
!         ZLWGKG=0.0_JPRB
!         ZIWGKG=0.0_JPRB
!       ENDIF
        ZLWGKG=0.0_JPRB
        ZIWGKG=0.0_JPRB
        IF (ZFCSW(JL,JCSW,JK) > REPSC ) THEN
          ZLWGKG=500.0_JPRB*(ABS(ZQLSW(JL,JCSW,JK))+ZQLSW(JL,JCSW,JK))
          ZIWGKG=500.0_JPRB*(ABS(ZQISW(JL,JCSW,JK))+ZQISW(JL,JCSW,JK))
        ENDIF
        ZRWGKG=0.0_JPRB
!       ZRAINT(JL)=0.0_JPRB  !  Not used in this loop

        ZDPOG=PDP(JL,JK)*ZRG
        ZFLWP(JL,JK)= ZLWGKG*ZDPOG
        ZFIWP(JL,JK)= ZIWGKG*ZDPOG
!       ZFRWP(JL,JK)= ZRWGKG*ZDPOG   ! Always zero
!       ZPODT=PAP(JL,JK)/(RD*PT(JL,JK)) ! Not used in this loop
!       ZLWC(JL,JK)=ZLWGKG*ZPODT    ! Not used in this loop
!       ZIWC(JL,JK)=ZIWGKG*ZPODT    ! Not used in this loop
      ENDDO


!          5.4    CLOUD SHORTWAVE OPTICAL PROPERTIES
!                 ----------------------------------
!   -------------------------
! --+ SW OPTICAL PARAMETERS +  Water clouds after Fouquart (1987)
!   -------------------------  Ice clouds (Ebert, Curry, 1992)

      DO JL = KIDIA,KFDIA
        ZTOSW(JL,JCSW,IJK) = 0.0_JPRB
        ZOMSW(JL,JCSW,IJK) = 1.0_JPRB
        ZASSW(JL,JCSW,IJK) = 0.0_JPRB
      ENDDO
      DO JL = KIDIA,KFDIA
        ZTOL=0.0_JPRB
        ZGL =0.0_JPRB
        ZOL =0.0_JPRB
        ZTOI=0.0_JPRB
        ZGI =0.0_JPRB
        ZOI =0.0_JPRB
        ZTOR=0.0_JPRB
        ZGR =0.0_JPRB
        ZOR =0.0_JPRB
!       IF (ZFLWP(JL,JK) > REPSCW .OR. ZFIWP(JL,JK) > REPSCW .OR. ZFRWP(JL,JK) > REPSCW ) THEN
        IF (ZFLWP(JL,JK) > REPSCW .OR. ZFIWP(JL,JK) > REPSCW) THEN
          IF (ZFLWP(JL,JK) > REPSCW ) THEN
            IF (NLIQOPT /= 0 ) THEN
!-- SW: Slingo, 1989
              ZTOL = ZFLWP(JL,JK)*(RSASWA(JSW)+RSASWB(JSW)/ZRADLI(JL,JK))
              ZGL  = RSASWE(JSW)+RSASWF(JSW)*ZRADLI(JL,JK)
              ZOL  = 1. - RSASWC(JSW)-RSASWD(JSW)*ZRADLI(JL,JK)
            ELSE
!-- SW: Fouquart, 1991
              ZTOL = ZFLWP(JL,JK)*(RSYFWA(JSW)+RSYFWB(JSW)/ZRADLI(JL,JK))
              ZGL  = RSYFWF(JSW)
              ZOL  = RSYFWC(JSW)-RSYFWD(JSW)*EXP(-RSYFWE(JSW)*ZTOL)
            ENDIF
          ENDIF

          IF (ZFIWP(JL,JK) > REPSCW ) THEN
            IF (NICEOPT <= 1) THEN
!-- SW: Ebert-Curry
              ZTOI = ZFIWP(JL,JK)*(RSECIA(JSW)+RSECIB(JSW)/ZRADIC(JL,JK))
              ZGI  = RSECIE(JSW)+RSECIF(JSW)*ZRADIC(JL,JK)
              ZOI  = 1.0_JPRB - RSECIC(JSW)-RSECID(JSW)*ZRADIC(JL,JK)

            ELSEIF (NICEOPT == 2) THEN
!-- SW: Fu-Liou 1993
              Z1RADI = 1.0_JPRB / ZDESIC(JL,JK)
              ZBETAI = RSFLA0(JSW)+Z1RADI* RSFLA1(JSW)
              ZTOI = ZFIWP(JL,JK) * ZBETAI
              ZOMGI= RSFLB0(JSW)+ZRADIC(JL,JK)*(RSFLB1(JSW) + ZRADIC(JL,JK) &
               & *(RSFLB2(JSW)+ZRADIC(JL,JK)* RSFLB3(JSW) ))
              ZOI  = 1.0_JPRB - ZOMGI
              ZOMGP= RSFLC0(JSW)+ZRADIC(JL,JK)*(RSFLC1(JSW) + ZRADIC(JL,JK) &
               & *(RSFLC2(JSW)+ZRADIC(JL,JK)* RSFLC3(JSW) ))
              ZFDEL= RSFLD0(JSW)+ZRADIC(JL,JK)*(RSFLD1(JSW) + ZRADIC(JL,JK) &
               & *(RSFLD2(JSW)+ZRADIC(JL,JK)* RSFLD3(JSW) ))
              ZGI  = ((1.0_JPRB-ZFDEL)*ZOMGP + ZFDEL*3.0_JPRB) *0.33333_JPRB

            ELSEIF (NICEOPT == 3) THEN
!-- SW: Fu 1996
              Z1RADI = 1.0_JPRB / ZDESIC(JL,JK)
              ZBETAI = RSFUA0(JSW)+Z1RADI* RSFUA1(JSW)
              ZTOI = ZFIWP(JL,JK) * ZBETAI
              ZOMGI= RSFUB0(JSW)+ZDESIC(JL,JK)*(RSFUB1(JSW) + ZDESIC(JL,JK) &
               & *(RSFUB2(JSW)+ZDESIC(JL,JK)* RSFUB3(JSW) ))
              ZOI  = 1.0_JPRB - ZOMGI
              ZGI  = RSFUC0(JSW)+ZDESIC(JL,JK)*(RSFUC1(JSW) + ZDESIC(JL,JK) &
               & *(RSFUC2(JSW)+ZDESIC(JL,JK)* RSFUC3(JSW) ))
              ZGI  = MIN(1.0_JPRB, ZGI)
            ENDIF
          ENDIF

!  - MIX of WATER and ICE CLOUDS
          ZTAUMX= ZTOL + ZTOI + ZTOR
          ZOMGMX= ZTOL*ZOL + ZTOI*ZOI + ZTOR*ZOR
          ZASYMX= ZTOL*ZOL*ZGL + ZTOI*ZOI*ZGI + ZTOR*ZOR*ZGR

          ZASYMX= ZASYMX/ZOMGMX
          ZOMGMX= ZOMGMX/ZTAUMX

! --- SW FINAL CLOUD OPTICAL PARAMETERS

          ZTOSW(JL,JCSW,IJK) = ZTAUMX
          ZOMSW(JL,JCSW,IJK) = ZOMGMX
          ZASSW(JL,JCSW,IJK) = ZASYMX
        ENDIF
      ENDDO
    ENDDO
  ENDDO

!          5.3    CLOUD LONGWAVE OPTICAL PROPERTIES FOR RRTM
!                 ------------------------------------------

!   -------------------------
! --+ LW OPTICAL PARAMETERS +  Water (and Ice) from Savijarvi (1998)
!   -------------------------  Ice clouds (Ebert, Curry, 1992)

! No need for a fixed diffusivity factor, accounted for spectrally below
! The detailed spectral structure does not require defining upward and
! downward effective optical properties

  DO JCLW=1,ICLW
    JRTM=NGB(JCLW)
    DO JK=1,KLEV
      IJK=KLEV+1-JK

!          5.4    CLOUD ICE AND LIQUID CONTENT AND PATH
!                 -------------------------------------

      DO JL = KIDIA,KFDIA

! --- LIQUID WATER CONTENT (g.m-3) AND LIQUID WATER PATH (g.m-2)
        IF (ZFCLW(JL,JCLW,JK) > REPSC ) THEN
          ZLWGKG=MAX(ZQLLW(JL,JCLW,JK)*1000.0_JPRB,0.0_JPRB)
          ZIWGKG=MAX(ZQILW(JL,JCLW,JK)*1000.0_JPRB,0.0_JPRB)
        ELSE
          ZLWGKG=0.0_JPRB
          ZIWGKG=0.0_JPRB
        ENDIF
        ZRWGKG=0.0_JPRB
!       ZRAINT(JL)=0.0_JPRB ! Not used in this loop

        ZDPOG=PDP(JL,JK)*ZRG
        ZFLWP(JL,JK)= ZLWGKG*ZDPOG
        ZFIWP(JL,JK)= ZIWGKG*ZDPOG
!       ZFRWP(JL,JK)= ZRWGKG*ZDPOG   ! Always zero
!       ZPODT=PAP(JL,JK)/(RD*PT(JL,JK)) ! Not used in this loop
!       ZLWC(JL,JK)=ZLWGKG*ZPODT  ! Not used in this loop
!       ZIWC(JL,JK)=ZIWGKG*ZPODT  ! Not used in this loop
      ENDDO


!          5.5    CLOUD SHORTWAVE OPTICAL PROPERTIES
!                 ----------------------------------

      DO JL = KIDIA,KFDIA
        ZRSALD = 0.0_JPRB
        ZRSAID = 0.0_JPRB

        IF (ZFLWP(JL,JK)+ZFIWP(JL,JK) > REPSCW) THEN

          IF (NLIQOPT == 0 .OR. NLIQOPT >= 3) THEN
! water cloud total emissivity a la Smith and Shi (1992)
            ZMULTL=1.2_JPRB-0.006_JPRB*ZRADLI(JL,JK)
            ZRSALD= 0.144_JPRB*ZMULTL *0.60240_JPRB

          ELSEIF (NLIQOPT == 1) THEN
! water cloud spectral emissivity a la Savijarvi (1997)
            ZRSALD= RHSAVI(JRTM,1) + ZRADLI(JL,JK)&
             & *(RHSAVI(JRTM,2) + ZRADLI(JL,JK)*RHSAVI(JRTM,3))

          ELSEIF (NLIQOPT == 2) THEN
! water cloud spectral emissivity a la Lindner and Li (2000)
            Z1RADL = 1.0_JPRB / ZRADLI(JL,JK)
          ZEXTCF = RLILIA(JRTM,1)+ZRADLI(JL,JK)*RLILIA(JRTM,2)+ Z1RADL*&
             & (RLILIA(JRTM,3) + Z1RADL*(RLILIA(JRTM,4) + Z1RADL*&
             & RLILIA(JRTM,5) ))
            Z1MOMG = RLILIB(JRTM,1) + Z1RADL*RLILIB(JRTM,2) &
             & + ZRADLI(JL,JK) *(RLILIB(JRTM,3) + ZRADLI(JL,JK)*RLILIB(JRTM,4) )
            ZRSALD = Z1MOMG * ZEXTCF
          ENDIF

          IF (NICEOPT == 0) THEN
! ice cloud spectral emissivity a la Smith & Shi (1992)
            ZMULTI=1.2_JPRB-0.006_JPRB*ZRADIC(JL,JK)
            ZRSAID= 0.103_JPRB*ZMULTI *0.60240_JPRB

          ELSEIF (NICEOPT == 1) THEN
! ice cloud spectral emissivity a la Ebert-Curry (1992)
            ZRSAID= REBCUH(JRTM)+REBCUG(JRTM)/ZRADIC(JL,JK)

          ELSEIF (NICEOPT == 2) THEN
! ice cloud spectral emissivity a la Fu & Liou (1993)
            Z1RADI= 1.0_JPRB / ZDESIC(JL,JK)
            ZRSAID = RFULIO(JRTM,1) + Z1RADI &
             & *(RFULIO(JRTM,2) + Z1RADI*RFULIO(JRTM,3))

          ELSEIF (NICEOPT == 3) THEN
! ice cloud spectral emissivity a la Fu et al (1998) including
! parametrisation for LW scattering effect
            Z1RADI = 1.0_JPRB / ZDESIC(JL,JK)
            ZRSAIE = RFUETA(JRTM,1) + Z1RADI&
             & *(RFUETA(JRTM,2) + Z1RADI*RFUETA(JRTM,3))
            ZRSAIA = Z1RADI*(RFUETB(JRTM,1) +ZDESIC(JL,JK)*( RFUETB(JRTM,2) +ZDESIC(JL,JK)*&
             & ( RFUETB(JRTM,3) +ZDESIC(JL,JK)* RFUETB(JRTM,4))))
            ZRSAIG = RFUETC(JRTM,1) +ZDESIC(JL,JK)*( RFUETC(JRTM,2) +ZDESIC(JL,JK)*&
             & ( RFUETC(JRTM,3) +ZDESIC(JL,JK)* RFUETC(JRTM,4)))
            ZRSAIF = 0.5_JPRB + ZRSAIG*( 0.3738_JPRB + ZRSAIG*( 0.0076_JPRB + ZRSAIG*0.1186_JPRB ) )
            ZRSAID = (1.0_JPRB - ZRSAIA/ZRSAIE * ZRSAIF) * ZRSAIE

          ENDIF

          ZTAUD = ZRSALD*ZFLWP(JL,JK)+ZRSAID*ZFIWP(JL,JK)

! Diffusivity correction within clouds a la Savijarvi
          IF (LDIFFC) THEN
            ZDIFFD=MIN(MAX(1.517_JPRB-0.156_JPRB*LOG(ZTAUD) , 1.0_JPRB) , 2.0_JPRB)
          ELSE
            ZDIFFD=1.66_JPRB
          ENDIF
          ZTOLW(JL,IJK,JCLW) = ZTAUD*ZDIFFD
        ENDIF

      ENDDO
    ENDDO
  ENDDO

ENDIF
print *,'RADLSWR after defining cloud optical properties McICA or not'
NUAER = NUA
NTRAER = NTRA

!     ------------------------------------------------------------------

!     ------------------------------------------------------------------

!*         6.     CALL LONGWAVE RADIATION CODE
!                 ----------------------------

!*         6.1    FULL LONGWAVE RADIATION COMPUTATIONS
!                 ------------------------------------

IF (.NOT.LPHYLIN) THEN
  IF ( .NOT. LRRTM) THEN

    STOP 'OLD LW SCHEME NOT AVAILABLE IN McICA CONFIGURATION'

  ELSE

!*         6.2    FULL LONGWAVE RADIATION COMPUTATIONS - RRTM
!                 ------------------------------------   ----

!  i)  pass ZOZN (ozone mass mixing ratio) to RRTM; remove pressure
!      weighting applied to POZON in driverMC (below)
!  ii) pass ZEMIS and ZEMIW to RRTM; return ZEMIT from RRTM
!  iii)pass ZTAUCLD, cloud optical depths (water+ice) to RRTM,
!      computed from equations above
!  iv) pass ECRT arrays to RRTM arrays in interface routine ECRTATM
!      in module rrtm_ecrt.f

    DO JL = KIDIA,KFDIA
      DO JK = 1, KLEV
        ZOZN(JL,JK) = POZON(JL,JK)/PDP(JL,JK)
      ENDDO
    ENDDO

!    JL=KIDIA
!    DO JK=1,KLEV
!      PRINT 9003,JK,PAPH(JL,JK),PAP(JL,JK),PTH(JL,JK),PT(JL,JK),PDP(JL,JK),PQ(JL,JK) &
!       &,PCO2(JL,JK),PCH4(JL,JK),PN2O(JL,JK),PC11(JL,JK),PC12(JL,JK),PC22(JL,JK),PCL4(JL,JK),ZOZN(JL,JK)
!9003  FORMAT(1X,I3,2F9.1,2F7.2,F9.1,9E10.3)
!    ENDDO

    IF (NMCICA == 0) THEN

     CALL RRTM_RRTM_140GP &
      & ( KIDIA , KFDIA , KLON  , KLEV,&
      & PAER  , PAPH  , PAP,&
      & PTS   , PTH   , PT,&
      & ZEMIS , ZEMIW,&
      & PQ    , PCO2  , PCH4 , PN2O   , PNO2, PC11, PC12, PC22, PCL4, ZOZN, ZCLDSW, ZTAUCLD,&
      & ZEMIT , ZFLUX , ZFLUC, ZTCLEAR &
      & )

    ELSE

      ICOLS=ICOLSLW

!     JL=KIDIA
!     DO JK=1,KLEV
!       IF (ZCLFR(JL,JK) > 0._JPRB) THEN
!         PRINT 9004,JK,ZCLFR(JL,JK),(ZLWFC(JL,ITLW,JK),ITLW=1,ICOLS)
!9004     FORMAT(1X,I3,2F7.4,140F2.0)
!       ENDIF
!     ENDDO


      CALL RRTM_RRTM_140GP_McICA &
       &( KIDIA, KFDIA, KLON , KLEV, ICOLS, ICLDLW ,&
       &  PAER , PAPH , PAP  , &
       &  PTS  , PTH  , PT   , &
       &  ZEMIS, ZEMIW, &
       &  PQ   , PCO2 , PCH4 , PN2O, PNO2 , PC11, PC12, PC22, PCL4, ZOZN , ZLWFC , ZTOLW , ZCLDLW, &
       &  ZEMIT, ZFLUX, ZFLUC &
       & )

    ENDIF

  ENDIF

  DO JK = 1 , KLEV+1
    IK=KLEV+2-JK
    DO JL=KIDIA,KFDIA
      PFLT(JL,JK) =-ZFLUX(JL,2,IK) - ZFLUX(JL,1,IK)
      PFCT(JL,JK) =-ZFLUC(JL,2,IK) - ZFLUC(JL,1,IK)
    ENDDO
  ENDDO

ELSE
  ZEMIT (:)   = 0.0_JPRB
  ZFLUX(:,:,:)= 0.0_JPRB
  ZFLUC(:,:,:)= 0.0_JPRB
ENDIF
print *,'RADLSWR after longwave computations'

!     ------------------------------------------------------------------

!*         7.     CALL SHORTWAVE RADIATION CODE
!                 -----------------------------

ZRMUZ=0.0_JPRB
DO JL = KIDIA,KFDIA
  ZRMUZ = MAX (ZRMUZ, ZMU0(JL))
ENDDO

IF (NMCICA == 0) THEN

  CALL SRTM_SRTM_224GP &
    & ( KIDIA , KFDIA , KLON  , KLEV  , ISW , NOVLP,&
    &   PAER  , ZSALBD, ZSALBP, PAPH  , PAP ,&
    &   PTS   , PTH   , PT    ,&
    &   PQ    , PCO2  , PCH4  , PN2O  , PNO2, ZOZN , ZMU0  ,&
    &   ZCLDSW, ZSTAUC, ZSASYC, ZSOMGC,&
    &   ZALBT , ZFSUX , ZFSUC , ZFUVF ,ZSUDU &
    & )

ELSE

  ICOLS=ICOLSSW

  CALL SRTM_SRTM_224GP_McICA &
   & ( KIDIA , KFDIA , KLON  , KLEV , ISW , ICOLS , ICLDSW ,&
   &   PAER  , ZSALBD, ZSALBP, PAPH , PAP ,&
   &   PTS   , PTH   , PT    , &
   &   PQ    , PCO2  , PCH4  , PN2O , PNO2, ZOZN  , ZMU0  ,&
   &   ZSWFC , ZTOSW , ZASSW , ZOMSW,&
   &   ZFSUX , ZFSUC , ZFUVF , ZFUVC,ZPARF, ZPARCF, ZSUDU &
   & )

ENDIF
print *,'RADLSWR after shortwave computations'
!     ------------------------------------------------------------------

!*         8.     FILL UP THE MODEL NET LW AND SW RADIATIVE FLUXES
!                 ------------------------------------------------

DO JK = 1 , KLEV+1
  IK=KLEV+2-JK
  DO JL = KIDIA,KFDIA
    PFLS(JL,JK) = ZFSUX(JL,2,JK) - ZFSUX(JL,1,JK)
    PFCS(JL,JK) = ZFSUC(JL,2,JK) - ZFSUC(JL,1,JK)
  ENDDO
ENDDO

DO JL = KIDIA,KFDIA
  PFRSOD(JL) = ZFSUX(JL,2,KLEV+1)
  PEMIT (JL) = ZEMIT(JL)
  PSUDU (JL) = ZSUDU(JL)
  PUVDF (JL) = ZFUVF(JL)
  PPARF (JL) = ZPARF(JL)
  PPARCF(JL) = ZPARCF(JL)
!  PDOWN(JL,1)= ZFLUX(JL,2,1)
!  PDOWN(JL,2)= ZFLUC(JL,2,1)
!  PDOWN(JL,3)= ZFSUX(JL,2,KLEV+1)
!  PDOWN(JL,4)= ZFSUC(JL,2,KLEV+1)
ENDDO
print *,'RADLSWR right at the end'
!     --------------------------------------------------------------

END SUBROUTINE RADLSWR
