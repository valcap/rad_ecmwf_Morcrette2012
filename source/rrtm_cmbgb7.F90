!***************************************************************************
SUBROUTINE RRTM_CMBGB7
!***************************************************************************

!     BAND 7:  980-1080 cm-1 (low - H2O,O3; high - O3)
!***************************************************************************

! Parameters
USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE YOERRTO7 , ONLY : KAO     ,KBO     ,SELFREFO   ,FRACREFAO  ,&
 & FRACREFBO, ABSCO2O  
USE YOERRTA7 , ONLY : KA      ,KB      ,SELFREF    ,FRACREFA   ,&
 & FRACREFB , ABSCO2  
USE YOERRTRWT, ONLY : FREFA    ,FREFB    ,FREFADF  ,RWGT
USE YOERRTFTR, ONLY : NGC      ,NGS      ,NGN      

IMPLICIT NONE

INTEGER(KIND=JPIM) :: IGC, IPR, IPRSM, JN, JP, JT

REAL(KIND=JPRB) :: Z_SUMF, Z_SUMK

DO JN = 1,9
  DO JT = 1,5
    DO JP = 1,13
      IPRSM = 0
      DO IGC = 1,NGC(7)
        Z_SUMK = 0.0_JPRB
        DO IPR = 1, NGN(NGS(6)+IGC)
          IPRSM = IPRSM + 1

          Z_SUMK = Z_SUMK + KAO(JN,JT,JP,IPRSM)*RWGT(IPRSM+96)
        ENDDO

        KA(JN,JT,JP,IGC) = Z_SUMK
      ENDDO
    ENDDO
  ENDDO
ENDDO
DO JT = 1,5
  DO JP = 13,59
    IPRSM = 0
    DO IGC = 1,NGC(7)
      Z_SUMK = 0.0_JPRB
      DO IPR = 1, NGN(NGS(6)+IGC)
        IPRSM = IPRSM + 1

        Z_SUMK = Z_SUMK + KBO(JT,JP,IPRSM)*RWGT(IPRSM+96)
      ENDDO

      KB(JT,JP,IGC) = Z_SUMK
    ENDDO
  ENDDO
ENDDO

DO JT = 1,10
  IPRSM = 0
  DO IGC = 1,NGC(7)
    Z_SUMK = 0.0_JPRB
    DO IPR = 1, NGN(NGS(6)+IGC)
      IPRSM = IPRSM + 1

      Z_SUMK = Z_SUMK + SELFREFO(JT,IPRSM)*RWGT(IPRSM+96)
    ENDDO

    SELFREF(JT,IGC) = Z_SUMK
  ENDDO
ENDDO

DO JP = 1,9
  IPRSM = 0
  DO IGC = 1,NGC(7)
    Z_SUMF = 0.0_JPRB
    DO IPR = 1, NGN(NGS(6)+IGC)
      IPRSM = IPRSM + 1

      Z_SUMF = Z_SUMF + FRACREFAO(IPRSM,JP)
    ENDDO

    FRACREFA(IGC,JP) = Z_SUMF
  ENDDO
ENDDO

IPRSM = 0
DO IGC = 1,NGC(7)
  Z_SUMF = 0.0_JPRB
  Z_SUMK = 0.0_JPRB
  DO IPR = 1, NGN(NGS(6)+IGC)
    IPRSM = IPRSM + 1

    Z_SUMF = Z_SUMF + FRACREFBO(IPRSM)
    Z_SUMK = Z_SUMK + ABSCO2O(IPRSM)*RWGT(IPRSM+96)
  ENDDO

  FRACREFB(IGC) = Z_SUMF
  ABSCO2(IGC) = Z_SUMK
ENDDO

DO JP = 1,9
  DO IGC = 1,NGC(7)

    FREFA(NGS(6)+IGC,JP) = FRACREFA(IGC,JP)
  ENDDO
ENDDO
DO JP = 1,8
  DO IGC = 1,NGC(7)

    FREFADF(NGS(6)+IGC,JP) = FRACREFA(IGC,JP+1) -FRACREFA(IGC,JP)
  ENDDO
ENDDO
DO IGC = 1,NGC(7)

  FREFB(NGS(6)+IGC,1) = FRACREFB(IGC)
ENDDO

END SUBROUTINE RRTM_CMBGB7
