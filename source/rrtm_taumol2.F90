!----------------------------------------------------------------------------
SUBROUTINE RRTM_TAUMOL2 (KIDIA,KFDIA,KLEV,P_TAU,P_COLDRY,&
 & P_TAUAERL,P_FAC00,P_FAC01,P_FAC10,P_FAC11,P_FORFAC,K_JP,K_JT,K_JT1,&
 & P_COLH2O,K_LAYTROP,P_SELFFAC,P_SELFFRAC,K_INDSELF,PFRAC)  

!     BAND 2:  250-500 cm-1 (low - H2O; high - H2O)

! Modifications
!        M.Hamrud      01-Oct-2003 CY28 Cleaning

!     D Salmond   2000-05-15 speed-up
!     JJMorcrette 2000-05-17 speed-up
!     JJMorcrette 2000-07-14 bugfix
!        NEC           25-Oct-2007 Optimisations

USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE PARRRTM  , ONLY : JPLAY  ,JPBAND ,JPGPT  ,NG2   ,NGS1
USE YOERRTWN , ONLY : NSPA   ,NSPB
USE YOERRTA2 , ONLY : ABSA   ,ABSB   ,FRACREFA, FRACREFB,&
 & FORREF   ,SELFREF , REFPARAM  
USE YOERRTBG2, ONLY : CORR1  ,CORR2

!  Input
!#include "yoeratm.h"

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_TAU(KIDIA:KFDIA,JPGPT,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLDRY(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_TAUAERL(KIDIA:KFDIA,JPLAY,JPBAND) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC00(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC01(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC10(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC11(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FORFAC(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JP(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT1(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLH2O(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_LAYTROP(KIDIA:KFDIA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFAC(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFRAC(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_INDSELF(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRAC(KIDIA:KFDIA,JPGPT,JPLAY) 
!  Output
!- from AER
!- from INTFAC      
!- from INTIND
!- from PROFDATA             
!- from SELF             
!- from SP             
REAL(KIND=JPRB) :: Z_FC00(JPLAY),Z_FC01(JPLAY),Z_FC10(JPLAY),Z_FC11(JPLAY)
!      REAL TAUAER(JPLAY)
REAL(KIND=JPRB) :: Z_FRACINT(JPLAY)
INTEGER(KIND=JPIM) :: IND0(JPLAY),IND1(JPLAY),INDS(JPLAY), INDEX(KIDIA:KFDIA,JPLAY)

INTEGER(KIND=JPIM) :: IFP, IFRAC, JG, JFRAC, JLAY
INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: Z_FP, Z_H2OPARAM, Z_WATER

!      EQUIVALENCE (TAUAERL(1,2),TAUAER)

!     Compute the optical depth by interpolating in ln(pressure) and 
!     temperature.  Below LAYTROP, the water vapor self-continuum is 
!     interpolated (in temperature) separately.


DO JLAY = 1, KLEV
  DO JLON = KIDIA, KFDIA
    IF (JLAY <= K_LAYTROP(JLON)) THEN
      Z_WATER = 1.E20_JPRB * P_COLH2O(JLON,JLAY) / P_COLDRY(JLON,JLAY)
      Z_H2OPARAM = Z_WATER/(Z_WATER +.002_JPRB)
  
!  DO IFRAC = 2, 12
!    IF (H2OPARAM  >=  REFPARAM(IFRAC)) GO TO 1900
!  ENDDO
!  1900 CONTINUE
!  FRACINT(LAY) = (H2OPARAM-REFPARAM(IFRAC))/&
!   &(REFPARAM(IFRAC-1)-REFPARAM(IFRAC))

      IF (Z_H2OPARAM >= REFPARAM(2)) THEN
        INDEX(JLON,JLAY)=2
      ELSE
!CDIR UNROLL=12
        DO JFRAC = 2, 12
          IF (Z_H2OPARAM < REFPARAM(JFRAC)) THEN
            INDEX(JLON,JLAY)=JFRAC+1
          ENDIF  
        ENDDO
      ENDIF  
  
!---- JJM_000714
      IFRAC=INDEX(JLON,JLAY)
      Z_FRACINT(JLAY) = (Z_H2OPARAM-REFPARAM(IFRAC))/&
       & (REFPARAM(IFRAC-1)-REFPARAM(IFRAC))  

      Z_FP = P_FAC11(JLON,JLAY) + P_FAC01(JLON,JLAY)
      IFP = 2.E2_JPRB*Z_FP+0.5_JPRB

!---MI 981104        
!       IF (IFP<=0) IFP=0

      IFP=MAX(0,IFP)

      Z_FC00(JLAY) = P_FAC00(JLON,JLAY) * CORR2(IFP)
      Z_FC10(JLAY) = P_FAC10(JLON,JLAY) * CORR2(IFP)
      Z_FC01(JLAY) = P_FAC01(JLON,JLAY) * CORR1(IFP)
      Z_FC11(JLAY) = P_FAC11(JLON,JLAY) * CORR1(IFP)
      IND0(JLAY) = ((K_JP(JLON,JLAY)-1)*5+(K_JT(JLON,JLAY)-1))*NSPA(2) + 1
      IND1(JLAY) = (K_JP(JLON,JLAY)*5+(K_JT1(JLON,JLAY)-1))*NSPA(2) + 1
      INDS(JLAY) = K_INDSELF(JLON,JLAY)

!-- DS_000515  
!CDIR UNROLL=NG2
      DO JG = 1, NG2
!-- DS_000515  
        P_TAU(JLON,NGS1+JG,JLAY) = P_COLH2O(JLON,JLAY) *&
         & (Z_FC00(JLAY) * ABSA(IND0(JLAY)  ,JG) +&
         & Z_FC10(JLAY) * ABSA(IND0(JLAY)+1,JG) +&
         & Z_FC01(JLAY) * ABSA(IND1(JLAY)  ,JG) +&
         & Z_FC11(JLAY) * ABSA(IND1(JLAY)+1,JG) +&
         & P_SELFFAC(JLON,JLAY) * (SELFREF(INDS(JLAY),JG) + &
         & P_SELFFRAC(JLON,JLAY) *&
         & (SELFREF(INDS(JLAY)+1,JG) - SELFREF(INDS(JLAY),JG)))&
         & + P_FORFAC(JLON,JLAY) * FORREF(JG) ) &
         & + P_TAUAERL(JLON,JLAY,2)  
        PFRAC(JLON,NGS1+JG,JLAY) = FRACREFA(JG,IFRAC) + Z_FRACINT(JLAY) *&
         & (FRACREFA(JG,IFRAC-1)-FRACREFA(JG,IFRAC))  
      ENDDO
    ENDIF
    IF (JLAY > K_LAYTROP(JLON)) THEN
      Z_FP = P_FAC11(JLON,JLAY) + P_FAC01(JLON,JLAY)
      IFP = 2.E2_JPRB*Z_FP+0.5_JPRB

!---MI 981104        
      IF (IFP <= 0) IFP=0

      Z_FC00(JLAY) = P_FAC00(JLON,JLAY) * CORR2(IFP)
      Z_FC10(JLAY) = P_FAC10(JLON,JLAY) * CORR2(IFP)
      Z_FC01(JLAY) = P_FAC01(JLON,JLAY) * CORR1(IFP)
      Z_FC11(JLAY) = P_FAC11(JLON,JLAY) * CORR1(IFP)
      IND0(JLAY) = ((K_JP(JLON,JLAY)-13)*5+(K_JT(JLON,JLAY)-1))*NSPB(2) + 1
      IND1(JLAY) = ((K_JP(JLON,JLAY)-12)*5+(K_JT1(JLON,JLAY)-1))*NSPB(2) + 1

!-- JJM_000517
!CDIR UNROLL=NG2
      DO JG = 1, NG2
!-- JJM_000517
        P_TAU(JLON,NGS1+JG,JLAY) = P_COLH2O(JLON,JLAY) *&
         & (Z_FC00(JLAY) * ABSB(IND0(JLAY)  ,JG) +&
         & Z_FC10(JLAY) * ABSB(IND0(JLAY)+1,JG) +&
         & Z_FC01(JLAY) * ABSB(IND1(JLAY)  ,JG) +&
         & Z_FC11(JLAY) * ABSB(IND1(JLAY)+1,JG)&
         & + P_FORFAC(JLON,JLAY) * FORREF(JG) ) &
         & + P_TAUAERL(JLON,JLAY,2)  
        PFRAC(JLON,NGS1+JG,JLAY) = FRACREFB(JG)
      ENDDO
    ENDIF
  ENDDO
ENDDO


END SUBROUTINE RRTM_TAUMOL2
