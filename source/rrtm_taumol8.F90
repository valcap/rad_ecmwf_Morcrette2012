!*******************************************************************************
SUBROUTINE RRTM_TAUMOL8 (KIDIA,KFDIA,KLEV,P_TAU,P_WX,&
 & P_TAUAERL,P_FAC00,P_FAC01,P_FAC10,P_FAC11,K_JP,K_JT,K_JT1,&
 & P_COLH2O,P_COLO3,P_COLN2O,P_CO2MULT,K_LAYSWTCH,P_SELFFAC,P_SELFFRAC,K_INDSELF,PFRAC)  

!     BAND 8:  1080-1180 cm-1 (low (i.e.>~300mb) - H2O; high - O3)

! Modifications
!        M.Hamrud      01-Oct-2003 CY28 Cleaning

!     D Salmond   2000-05-15 speed-up
!     JJMorcrette 2000-05-17 speed-up
!        NEC           25-Oct-2007 Optimisations

USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE PARRRTM  , ONLY : JPLAY  ,JPBAND ,JPGPT  ,JPXSEC ,NG8   ,NGS7
USE YOERRTWN , ONLY : NSPA   ,NSPB
USE YOERRTA8 , ONLY : ABSA   ,ABSB   ,FRACREFA, FRACREFB,SELFREF,ABSCO2A , ABSCO2B ,&
 & ABSN2OA , ABSN2OB,CFC12  ,CFC22ADJ, H2OREF  ,N2OREF  , O3REF  

!  Input
!#include "yoeratm.h"

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_TAU(KIDIA:KFDIA,JPGPT,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_WX(KIDIA:KFDIA,JPXSEC,JPLAY) ! Amount of trace gases
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_TAUAERL(KIDIA:KFDIA,JPLAY,JPBAND) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC00(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC01(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC10(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC11(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JP(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT1(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLH2O(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLO3(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLN2O(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_CO2MULT(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_LAYSWTCH(KIDIA:KFDIA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFAC(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFRAC(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_INDSELF(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRAC(KIDIA:KFDIA,JPGPT,JPLAY) 
!  Output
!- from AER
!- from INTFAC      
!- from INTIND
!- from PROFDATA             
!- from SELF             
!- from SP             
INTEGER(KIND=JPIM) :: IND0(JPLAY),IND1(JPLAY),INDS(JPLAY)

!      REAL TAUAER(JPLAY)
REAL(KIND=JPRB) :: Z_N2OMULT(JPLAY)

INTEGER(KIND=JPIM) :: JG, JLAY
INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: Z_COLREF1, Z_COLREF2, Z_CURRN2O, Z_FP, Z_RATIO, Z_WCOMB1, Z_WCOMB2

!      EQUIVALENCE (TAUAERL(1,8),TAUAER)

!     Compute the optical depth by interpolating in ln(pressure) and 
!     temperature.  


DO JLAY = 1, KLEV
  DO JLON = KIDIA, KFDIA
    IF (JLAY <= K_LAYSWTCH(JLON)) THEN
      Z_FP = P_FAC01(JLON,JLAY) + P_FAC11(JLON,JLAY)
      IND0(JLAY) = ((K_JP(JLON,JLAY)-1)*5+(K_JT(JLON,JLAY)-1))*NSPA(8) + 1
      IND1(JLAY) = (K_JP(JLON,JLAY)*5+(K_JT1(JLON,JLAY)-1))*NSPA(8) + 1
      INDS(JLAY) = K_INDSELF(JLON,JLAY)
      Z_COLREF1 = N2OREF(K_JP(JLON,JLAY))
      Z_COLREF2 = N2OREF(K_JP(JLON,JLAY)+1)
      Z_WCOMB1 = 1.0_JPRB/H2OREF(K_JP(JLON,JLAY))
      Z_WCOMB2 = 1.0_JPRB/H2OREF(K_JP(JLON,JLAY)+1)
      Z_RATIO = (Z_COLREF1*Z_WCOMB1)+Z_FP*((Z_COLREF2*Z_WCOMB2)-(Z_COLREF1*Z_WCOMB1))
      Z_CURRN2O = P_COLH2O(JLON,JLAY) * Z_RATIO
      Z_N2OMULT(JLAY) = P_COLN2O(JLON,JLAY) - Z_CURRN2O

!-- DS_000515
!CDIR UNROLL=NG8
      DO JG = 1, NG8
!-- DS_000515
        P_TAU(JLON,NGS7+JG,JLAY) = P_COLH2O(JLON,JLAY) *&
         & (P_FAC00(JLON,JLAY) * ABSA(IND0(JLAY)  ,JG) +&
         & P_FAC10(JLON,JLAY) * ABSA(IND0(JLAY)+1,JG) +&
         & P_FAC01(JLON,JLAY) * ABSA(IND1(JLAY)  ,JG) +&
         & P_FAC11(JLON,JLAY) * ABSA(IND1(JLAY)+1,JG) +&
         & P_SELFFAC(JLON,JLAY) * (SELFREF(INDS(JLAY),JG) + &
         & P_SELFFRAC(JLON,JLAY) *&
         & (SELFREF(INDS(JLAY)+1,JG) - SELFREF(INDS(JLAY),JG))))&
         & + P_WX(JLON,3,JLAY) * CFC12(JG)&
         & + P_WX(JLON,4,JLAY) * CFC22ADJ(JG)&
         & + P_CO2MULT(JLON,JLAY) * ABSCO2A(JG)&
         & + Z_N2OMULT(JLAY) * ABSN2OA(JG)&
         & + P_TAUAERL(JLON,JLAY,8)  
        PFRAC(JLON,NGS7+JG,JLAY) = FRACREFA(JG)
      ENDDO
    ENDIF

    IF (JLAY > K_LAYSWTCH(JLON)) THEN
      Z_FP = P_FAC01(JLON,JLAY) + P_FAC11(JLON,JLAY)
      IND0(JLAY) = ((K_JP(JLON,JLAY)-7)*5+(K_JT(JLON,JLAY)-1))*NSPB(8) + 1
      IND1(JLAY) = ((K_JP(JLON,JLAY)-6)*5+(K_JT1(JLON,JLAY)-1))*NSPB(8) + 1
      Z_COLREF1 = N2OREF(K_JP(JLON,JLAY))
      Z_COLREF2 = N2OREF(K_JP(JLON,JLAY)+1)
      Z_WCOMB1 = 1.0_JPRB/O3REF(K_JP(JLON,JLAY))
      Z_WCOMB2 = 1.0_JPRB/O3REF(K_JP(JLON,JLAY)+1)
      Z_RATIO = (Z_COLREF1*Z_WCOMB1)+Z_FP*((Z_COLREF2*Z_WCOMB2)-(Z_COLREF1*Z_WCOMB1))
      Z_CURRN2O = P_COLO3(JLON,JLAY) * Z_RATIO
      Z_N2OMULT(JLAY) = P_COLN2O(JLON,JLAY) - Z_CURRN2O

!-- JJM_000517
!CDIR UNROLL=NG8
      DO JG = 1, NG8
!-- JJM_000517
        P_TAU(JLON,NGS7+JG,JLAY) = P_COLO3(JLON,JLAY) *&
         & (P_FAC00(JLON,JLAY) * ABSB(IND0(JLAY)  ,JG) +&
         & P_FAC10(JLON,JLAY) * ABSB(IND0(JLAY)+1,JG) +&
         & P_FAC01(JLON,JLAY) * ABSB(IND1(JLAY)  ,JG) +&
         & P_FAC11(JLON,JLAY) * ABSB(IND1(JLAY)+1,JG)) &
         & + P_WX(JLON,3,JLAY) * CFC12(JG)&
         & + P_WX(JLON,4,JLAY) * CFC22ADJ(JG)&
         & + P_CO2MULT(JLON,JLAY) * ABSCO2B(JG)&
         & + Z_N2OMULT(JLAY) * ABSN2OB(JG)&
         & + P_TAUAERL(JLON,JLAY,8)  
        PFRAC(JLON,NGS7+JG,JLAY) = FRACREFB(JG)
      ENDDO
    ENDIF
  ENDDO
ENDDO


END SUBROUTINE RRTM_TAUMOL8
