!----------------------------------------------------------------------------
SUBROUTINE RRTM_TAUMOL9 (KIDIA,KFDIA,KLEV,P_TAU,&
 & P_TAUAERL,P_FAC00,P_FAC01,P_FAC10,P_FAC11,K_JP,K_JT,K_JT1,P_ONEMINUS,&
 & P_COLH2O,P_COLN2O,P_COLCH4,K_LAYTROP,K_LAYSWTCH,K_LAYLOW,P_SELFFAC,P_SELFFRAC,K_INDSELF,PFRAC)  

!     BAND 9:  1180-1390 cm-1 (low - H2O,CH4; high - CH4)

! Modifications
!        M.Hamrud      01-Oct-2003 CY28 Cleaning

!     D Salmond   2000-05-15 speed-up
!     JJMorcrette 2000-05-17 speed-up
!        NEC           25-Oct-2007 Optimisations

USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE PARRRTM  , ONLY : JPLAY  ,JPBAND ,JPGPT  ,NG9   ,NGS8
USE YOERRTWN , ONLY :      NSPA   ,NSPB
USE YOERRTA9 , ONLY : ABSA   ,ABSB   ,FRACREFA, FRACREFB,SELFREF,ABSN2O  , CH4REF  ,&
 & ETAREF  , H2OREF ,N2OREF ,STRRAT  

!  Input
!#include "yoeratm.h"

IMPLICIT NONE

!  Output
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_TAU(KIDIA:KFDIA,JPGPT,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_TAUAERL(KIDIA:KFDIA,JPLAY,JPBAND) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC00(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC01(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC10(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_FAC11(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JP(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_JT1(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_ONEMINUS
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLH2O(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLN2O(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_COLCH4(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_LAYTROP(KIDIA:KFDIA) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_LAYSWTCH(KIDIA:KFDIA) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_LAYLOW(KIDIA:KFDIA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFAC(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: P_SELFFRAC(KIDIA:KFDIA,JPLAY) 
INTEGER(KIND=JPIM),INTENT(IN)    :: K_INDSELF(KIDIA:KFDIA,JPLAY) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRAC(KIDIA:KFDIA,JPGPT,JPLAY) 
!- from AER
!- from INTFAC      
!- from INTIND
!- from PRECISE             
!- from PROFDATA             
!- from SELF             
!- from SP             
INTEGER(KIND=JPIM) :: JFRAC(JPLAY)
REAL(KIND=JPRB) :: Z_FFRAC(JPLAY),ZFS(JPLAY),Z_SPECCOMB(JPLAY)
INTEGER(KIND=JPIM) :: IND0(JPLAY),IND1(JPLAY),INDS(JPLAY),IIOFF(KIDIA:KFDIA,JPLAY)

!      REAL TAUAER(JPLAY)
REAL(KIND=JPRB) :: Z_N2OMULT(JPLAY)

INTEGER(KIND=JPIM) :: JG, IOFF(KIDIA:KFDIA), JS, JLAY, I_NS
INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: Z_COLREF1, Z_COLREF2, Z_CURRN2O, Z_FAC000, Z_FAC001,&
 & Z_FAC010, Z_FAC011, Z_FAC100, Z_FAC101, Z_FAC110, Z_FAC111, &
 & Z_FP, Z_FS, Z_RATIO, Z_SPECMULT, Z_SPECPARM, Z_WCOMB1, &
 & Z_WCOMB2  

!      EQUIVALENCE (TAUAERL(1,9),TAUAER)


DO JLON = KIDIA, KFDIA
  IOFF(JLON) = 0
ENDDO

DO JLAY = 1, KLEV
  DO JLON = KIDIA, KFDIA

!     Compute the optical depth by interpolating in ln(pressure), 
!     temperature, and appropriate species.  Below LAYTROP, the water
!     vapor self-continuum is interpolated (in temperature) separately.
  
    IF (JLAY <= K_LAYTROP(JLON)) THEN
      Z_SPECCOMB(JLAY) = P_COLH2O(JLON,JLAY) + STRRAT*P_COLCH4(JLON,JLAY)
      Z_SPECPARM = P_COLH2O(JLON,JLAY)/Z_SPECCOMB(JLAY)
      Z_SPECPARM=MIN(P_ONEMINUS,Z_SPECPARM)
      Z_SPECMULT = 8._JPRB*(Z_SPECPARM)
      JS = 1 + INT(Z_SPECMULT)
      JFRAC(JLAY) = JS
      Z_FS = MOD(Z_SPECMULT,1.0_JPRB)
      Z_FFRAC(JLAY) = Z_FS
      IF (JS  ==  8) THEN
        IF (Z_FS <= 0.68_JPRB) THEN
          Z_FS = Z_FS/0.68_JPRB
        ELSEIF (Z_FS  <=  0.92_JPRB) THEN
          JS = JS + 1
          Z_FS = (Z_FS-0.68_JPRB)/0.24_JPRB
        ELSE
          JS = JS + 2
          Z_FS = (Z_FS-0.92_JPRB)/0.08_JPRB
        ENDIF
      ELSEIF (JS  == 9) THEN
        JS = 10
        Z_FS = 1.0_JPRB
        JFRAC(JLAY) = 8
        Z_FFRAC(JLAY) = 1.0_JPRB
      ENDIF
      Z_FP = P_FAC01(JLON,JLAY) + P_FAC11(JLON,JLAY)
      I_NS = JS + INT(Z_FS + 0.5_JPRB)
      IND0(JLAY) = ((K_JP(JLON,JLAY)-1)*5+(K_JT(JLON,JLAY)-1))*NSPA(9) + JS
      IND1(JLAY) = (K_JP(JLON,JLAY)*5+(K_JT1(JLON,JLAY)-1))*NSPA(9) + JS
      INDS(JLAY) = K_INDSELF(JLON,JLAY)
      IF (JLAY  ==  K_LAYLOW(JLON)) IOFF(JLON) = NG9
      IF (JLAY  ==  K_LAYSWTCH(JLON)) IOFF(JLON) = 2*NG9
      Z_COLREF1 = N2OREF(K_JP(JLON,JLAY))
      Z_COLREF2 = N2OREF(K_JP(JLON,JLAY)+1)
      IF (I_NS  ==  11) THEN
        Z_WCOMB1 = 1.0_JPRB/H2OREF(K_JP(JLON,JLAY))
        Z_WCOMB2 = 1.0_JPRB/H2OREF(K_JP(JLON,JLAY)+1)
      ELSE
        Z_WCOMB1 = (1.0_JPRB-ETAREF(I_NS))/(STRRAT * CH4REF(K_JP(JLON,JLAY)))
        Z_WCOMB2 = (1.0_JPRB-ETAREF(I_NS))/(STRRAT * CH4REF(K_JP(JLON,JLAY)+1))
      ENDIF
      Z_RATIO = (Z_COLREF1*Z_WCOMB1)+Z_FP*((Z_COLREF2*Z_WCOMB2)-(Z_COLREF1*Z_WCOMB1))
      Z_CURRN2O = Z_SPECCOMB(JLAY) * Z_RATIO
      Z_N2OMULT(JLAY) = P_COLN2O(JLON,JLAY) - Z_CURRN2O

      ZFS(JLAY)=Z_FS
      IIOFF(JLON,JLAY)=IOFF(JLON)

!-- DS_000515
!CDIR UNROLL=NG9
      DO JG = 1, NG9
!-- DS_000515

        Z_FS=ZFS(JLAY)
        IOFF(JLON)=IIOFF(JLON,JLAY)
!---jjm
!    FAC000 = (_ONE_ - FS) * FAC00(LAY)
!    FAC010 = (_ONE_ - FS) * FAC10(LAY)
!    FAC100 = FS * FAC00(LAY)
!    FAC110 = FS * FAC10(LAY)
!    FAC001 = (_ONE_ - FS) * FAC01(LAY)
!    FAC011 = (_ONE_ - FS) * FAC11(LAY)
!    FAC101 = FS * FAC01(LAY)
!    FAC111 = FS * FAC11(LAY)
!------         

        P_TAU(JLON,NGS8+JG,JLAY) = Z_SPECCOMB(JLAY) *&
     !-- DS_000515
     !     &(Z_FAC000 * ABSA(IND0(JLAY)   ,JG) +&
     !     & Z_FAC100 * ABSA(IND0(JLAY)+ 1,JG) +&
     !     & Z_FAC010 * ABSA(IND0(JLAY)+11,JG) +&
     !     & Z_FAC110 * ABSA(IND0(JLAY)+12,JG) +&
     !     & Z_FAC001 * ABSA(IND1(JLAY)   ,JG) +&
     !     & Z_FAC101 * ABSA(IND1(JLAY)+ 1,JG) +&
     !     & Z_FAC011 * ABSA(IND1(JLAY)+11,JG) +&
     !     & Z_FAC111 * ABSA(IND1(JLAY)+12,JG))+&
         & ( (1. - Z_FS) *(P_FAC00(JLON,JLAY) * ABSA(IND0(JLAY)   ,JG) +   &
         & P_FAC10(JLON,JLAY) * ABSA(IND0(JLAY)+11,JG) +   &
         & P_FAC01(JLON,JLAY) * ABSA(IND1(JLAY)   ,JG) +   &
         & P_FAC11(JLON,JLAY) * ABSA(IND1(JLAY)+11,JG))+   &
         & Z_FS     *(P_FAC00(JLON,JLAY) * ABSA(IND0(JLAY)+ 1,JG) +   &
         & P_FAC10(JLON,JLAY) * ABSA(IND0(JLAY)+12,JG) +   &
         & P_FAC01(JLON,JLAY) * ABSA(IND1(JLAY)+ 1,JG) +   &
         & P_FAC11(JLON,JLAY) * ABSA(IND1(JLAY)+12,JG))) + &
     !-- DS_000515
         & P_COLH2O(JLON,JLAY) * &
         & P_SELFFAC(JLON,JLAY) * (SELFREF(INDS(JLAY),JG) + &
         & P_SELFFRAC(JLON,JLAY) *&
         & (SELFREF(INDS(JLAY)+1,JG) - SELFREF(INDS(JLAY),JG)))&
         & + Z_N2OMULT(JLAY) * ABSN2O(JG+IOFF(JLON))&
         & + P_TAUAERL(JLON,JLAY,9)  
        PFRAC(JLON,NGS8+JG,JLAY) = FRACREFA(JG,JFRAC(JLAY)) + Z_FFRAC(JLAY) *&
         & (FRACREFA(JG,JFRAC(JLAY)+1) - FRACREFA(JG,JFRAC(JLAY)))  
      ENDDO
    ENDIF

    IF (JLAY > K_LAYTROP(JLON)) THEN
      IND0(JLAY) = ((K_JP(JLON,JLAY)-13)*5+(K_JT(JLON,JLAY)-1))*NSPB(9) + 1
      IND1(JLAY) = ((K_JP(JLON,JLAY)-12)*5+(K_JT1(JLON,JLAY)-1))*NSPB(9) + 1

!-- JJM_000517
!CDIR UNROLL=NG9
      DO JG = 1, NG9
!-- JJM_000517
        P_TAU(JLON,NGS8+JG,JLAY) = P_COLCH4(JLON,JLAY) *&
         & (P_FAC00(JLON,JLAY) * ABSB(IND0(JLAY)  ,JG) +&
         & P_FAC10(JLON,JLAY) * ABSB(IND0(JLAY)+1,JG) +&
         & P_FAC01(JLON,JLAY) * ABSB(IND1(JLAY)  ,JG) +&
         & P_FAC11(JLON,JLAY) * ABSB(IND1(JLAY)+1,JG))&
         & + P_TAUAERL(JLON,JLAY,9)  
        PFRAC(JLON,NGS8+JG,JLAY) = FRACREFB(JG)
      ENDDO
    ENDIF
  ENDDO
ENDDO


END SUBROUTINE RRTM_TAUMOL9
